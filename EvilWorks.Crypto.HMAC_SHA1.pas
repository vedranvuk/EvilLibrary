(*============================================================================================================

 EvilLibrary by Vedran Vuk 2010-2012

 Name: 					EvilWorks.Crypto.HMAC_SHA1
 Description: 			HMAC_SHA1 hasher. Taken from "Fundamentals 4.00" chash.pas library version 4.15.
 Original Copyright:    Copyright © 1999-2011, David J Butler
 Original Home page:    http://fundementals.sourceforge.net
 Original Forum:        http://sourceforge.net/forum/forum.php?forum_id=2117
 Original E-mail:       fundamentalslib at gmail.com
 File last change date: August 15th. 2012
 File version: 			0.0.1
 Licence:				Free as in beer.

 ===========================================================================================================*)

unit EvilWorks.Crypto.HMAC_SHA1;

{$EXTENDEDSYNTAX ON}
{$IOCHECKS ON}
{$LONGSTRINGS ON}
{$BOOLEVAL OFF}
{$WRITEABLECONST OFF}
{$MINENUMSIZE 1}
{$OPTIMIZATION ON}
{$INLINE ON}
{$HIGHCHARUNICODE OFF}

{                                                                              }
{ Windows platform                                                             }
{                                                                              }
{$IFDEF DOT_NET}
{$DEFINE WindowsPlatform}
{$ENDIF}
{$IFDEF OS_WIN32}
{$DEFINE WindowsPlatform}
{$ENDIF}
{$IFDEF OS_WIN64}
{$DEFINE WindowsPlatform}
{$ENDIF}

{                                                                              }
{ CPU type                                                                     }
{                                                                              }
{$IFNDEF ManagedCode}
{$DEFINE NativeCode}
{$ENDIF}
{$IFDEF CPU386}
{$DEFINE INTEL386}
{$DEFINE CPU_INTEL386}
{$ENDIF}
{$IFDEF CPUX64}
{$DEFINE CPU_X86_64}
{$ENDIF}
{$IFDEF CPU86_64}
{$DEFINE CPU_X86_64}
{$ENDIF}
{$IFDEF CPU68K}
{$DEFINE CPU_68K}
{$ENDIF}
{$IFDEF CPUPPC}
{$DEFINE CPU_POWERPC}
{$ENDIF}
{$IFDEF CPUPPC64}
{$DEFINE CPU_POWERPC64}
{$ENDIF}
{$IFDEF CPUARM}
{$DEFINE CPU_ARM}
{$ENDIF}

{                                                                              }
{ Assembler style                                                              }
{                                                                              }
{$IFNDEF PurePascal}
{$IFNDEF ManagedCode}
{$IFDEF CPU_X86_64}
{$DEFINE ASMX86_64}
{$ENDIF}
{$IFDEF CPU_INTEL386}
{$DEFINE ASM386}
{$IFDEF DELPHI}{$IFDEF OS_WIN32}
{$DEFINE ASM386_DELPHI}
{$IFNDEF UseInline} {$DEFINE ASM386_DELPHI_INLINE_OFF} {$ENDIF}
{$ENDIF}{$ENDIF}
{$IFDEF FREEPASCAL2_UP}
{$DEFINE ASM386_FREEPASCAL}
{$ENDIF}
{$ENDIF}
{$ENDIF}
{$ENDIF}

{                                                                              }
{ Function inlining                                                            }
{                                                                              }
{$IFDEF SupportInline}
{$IFNDEF SupportInlineIsBuggy}
{$IFNDEF PurePascal}
{$DEFINE UseInline}
{$ENDIF}
{$ENDIF}
{$ENDIF}

{                                                                              }
{ Standard compiler directives                                                 }
{                                                                              }
{$EXTENDEDSYNTAX ON}
{$IOCHECKS ON}
{$LONGSTRINGS ON}
{$BOOLEVAL OFF}
{$WRITEABLECONST OFF}
{$MINENUMSIZE 1}
{$IFDEF DEBUG}
{$ASSERTIONS ON}
{$DEBUGINFO ON}
{$OVERFLOWCHECKS ON}
{$RANGECHECKS ON}
{$WARNINGS ON}
{$HINTS ON}
{$ELSE}
{$ASSERTIONS OFF}
{$DEBUGINFO OFF}
{$OVERFLOWCHECKS OFF}
{$RANGECHECKS OFF}
{$WARNINGS OFF}
{$HINTS OFF}
{$ENDIF}
{$IFDEF CLR}
{$UNSAFECODE OFF}
{$ENDIF}
{$IFDEF DELPHI}
{$OPTIMIZATION ON}
{$ENDIF}
{$IFDEF DELPHI2005_UP}
{$INLINE ON}
{$ENDIF}
{$IFDEF DELPHI2009_UP}
{$HIGHCHARUNICODE OFF}
{$ENDIF}

{                                                                              }
{ Compiler warnings                                                            }
{                                                                              }
{$IFDEF DELPHI7}
{$WARN UNSAFE_CODE OFF}
{$WARN UNSAFE_TYPE OFF}
{$WARN UNSAFE_CAST OFF}
{$ENDIF}

{$IFDEF DELPHI2007}
{$IFNDEF DOT_NET}
{$WARN UNSAFE_CODE OFF}
{$WARN UNSAFE_TYPE OFF}
{$WARN UNSAFE_CAST OFF}
{$ENDIF}
{$ENDIF}

{$IFDEF DOT_NET}
{$WARN UNIT_PLATFORM OFF}
{$ENDIF}

{$IFNDEF DEBUG}
{$IFDEF DELPHI6_UP}
{$WARN SYMBOL_PLATFORM OFF}
{$WARN UNIT_PLATFORM OFF}
{$WARN UNIT_DEPRECATED OFF}
{$ENDIF}
{$ENDIF}

interface

uses
	WinApi.Windows,
	System.SysUtils,
	EvilWorks.System.SysUtils;

{                                                                              }
{ Hash digests                                                                 }
{                                                                              }
type
	Word64 = packed record
		case Integer of
			0:
			(Bytes: array [0 .. 7] of Byte);
			1:
			(Words: array [0 .. 3] of Word);
			2:
			(LongWords: array [0 .. 1] of LongWord);
	end;

	PWord64 = ^Word64;

	T128BitDigest = record
		case integer of
			0:
			(Int64s: array [0 .. 1] of Int64);
			1:
			(Longs: array [0 .. 3] of LongWord);
			2:
			(Words: array [0 .. 7] of Word);
			3:
			(Bytes: array [0 .. 15] of Byte);
	end;

	P128BitDigest = ^T128BitDigest;

	T160BitDigest = record
		case integer of
			0:
			(Longs: array [0 .. 4] of LongWord);
			1:
			(Words: array [0 .. 9] of Word);
			2:
			(Bytes: array [0 .. 19] of Byte);
	end;

	P160BitDigest = ^T160BitDigest;

	T224BitDigest = record
		case integer of
			0:
			(Longs: array [0 .. 6] of LongWord);
			1:
			(Words: array [0 .. 13] of Word);
			2:
			(Bytes: array [0 .. 27] of Byte);
	end;

	P224BitDigest = ^T224BitDigest;

	T256BitDigest = record
		case integer of
			0:
			(Longs: array [0 .. 7] of LongWord);
			1:
			(Words: array [0 .. 15] of Word);
			2:
			(Bytes: array [0 .. 31] of Byte);
	end;

	P256BitDigest = ^T256BitDigest;

	T384BitDigest = record
		case integer of
			0:
			(Word64s: array [0 .. 5] of Word64);
			1:
			(Longs: array [0 .. 11] of LongWord);
			2:
			(Words: array [0 .. 23] of Word);
			3:
			(Bytes: array [0 .. 47] of Byte);
	end;

	P384BitDigest = ^T384BitDigest;

	T512BitDigest = record
		case integer of
			0:
			(Word64s: array [0 .. 7] of Word64);
			1:
			(Longs: array [0 .. 15] of LongWord);
			2:
			(Words: array [0 .. 31] of Word);
			3:
			(Bytes: array [0 .. 63] of Byte);
	end;

	P512BitDigest = ^T512BitDigest;
	T512BitBuf    = array [0 .. 63] of Byte;
	T1024BitBuf   = array [0 .. 127] of Byte;

const
	MaxHashDigestSize = Sizeof(T160BitDigest);

procedure DigestToHexBufA(const Digest; const Size: Integer; const Buf);
procedure DigestToHexBufW(const Digest; const Size: Integer; const Buf);
function DigestToHexA(const Digest; const Size: Integer): AnsiString;
function DigestToHexW(const Digest; const Size: Integer): WideString;
function Digest128Equal(const Digest1, Digest2: T128BitDigest): Boolean;
function Digest160Equal(const Digest1, Digest2: T160BitDigest): Boolean;
function Digest224Equal(const Digest1, Digest2: T224BitDigest): Boolean;
function Digest256Equal(const Digest1, Digest2: T256BitDigest): Boolean;
function Digest384Equal(const Digest1, Digest2: T384BitDigest): Boolean;
function Digest512Equal(const Digest1, Digest2: T512BitDigest): Boolean;

{                                                                              }
{ Hash errors                                                                  }
{                                                                              }
const
	hashNoError            = 0;
	hashInternalError      = 1;
	hashInvalidHashType    = 2;
	hashInvalidBuffer      = 3;
	hashInvalidBufferSize  = 4;
	hashInvalidDigest      = 5;
	hashInvalidKey         = 6;
	hashInvalidFileName    = 7;
	hashFileOpenError      = 8;
	hashFileSeekError      = 9;
	hashFileReadError      = 10;
	hashNotKeyedHashType   = 11;
	hashTooManyOpenHandles = 12;
	hashInvalidHandle      = 13;
	hashMAX_ERROR          = 13;

function GetHashErrorMessage(const ErrorCode: LongWord): PChar;

type
	EHashError = class(Exception)
	protected
		FErrorCode: LongWord;

	public
		constructor Create(const ErrorCode: LongWord; const Msg: string = '');
		property ErrorCode: LongWord read FErrorCode;
	end;

{                                                                              }
{ Secure memory clear                                                          }
{   Used to clear keys and other sensitive data from memory                    }
{                                                                              }
procedure SecureClear(var Buf; const BufSize: Integer);
procedure SecureClear512(var Buf: T512BitBuf);
procedure SecureClear1024(var Buf: T1024BitBuf);
procedure SecureClearStrA(var S: AnsiString);
procedure SecureClearStrW(var S: WideString);

{                                                                              }
{ Checksum hashing                                                             }
{                                                                              }
function CalcChecksum32(const Buf; const BufSize: Integer): LongWord; overload;
function CalcChecksum32(const Buf: AnsiString): LongWord; overload;

{                                                                              }
{ XOR hashing                                                                  }
{                                                                              }
function CalcXOR8(const Buf; const BufSize: Integer): Byte; overload;
function CalcXOR8(const Buf: AnsiString): Byte; overload;

function CalcXOR16(const Buf; const BufSize: Integer): Word; overload;
function CalcXOR16(const Buf: AnsiString): Word; overload;

function CalcXOR32(const Buf; const BufSize: Integer): LongWord; overload;
function CalcXOR32(const Buf: AnsiString): LongWord; overload;

{                                                                              }
{ CRC 16 hashing                                                               }
{                                                                              }
{   The theory behind CCITT V.41 CRCs:                                         }
{                                                                              }
{      1. Select the magnitude of the CRC to be used (typically 16 or 32       }
{         bits) and choose the polynomial to use. In the case of 16 bit        }
{         CRCs, the CCITT polynomial is recommended and is                     }
{                                                                              }
{                       16    12    5                                          }
{               G(x) = x   + x   + x  + 1                                      }
{                                                                              }
{         This polynomial traps 100% of 1 bit, 2 bit, odd numbers of bit       }
{         errors, 100% of <= 16 bit burst errors and over 99% of all           }
{         other errors.                                                        }
{                                                                              }
{      2. The CRC is calculated as                                             }
{                               r                                              }
{               D(x) = (M(x) * 2 )  mod G(x)                                   }
{                                                                              }
{         This may be better described as : Add r bits (0 content) to          }
{         the end of M(x). Divide this by G(x) and the remainder is the        }
{         CRC.                                                                 }
{                                                                              }
{      3. Tag the CRC onto the end of M(x).                                    }
{                                                                              }
{      4. To check it, calculate the CRC of the new message D(x), using        }
{         the same process as in 2. above. The newly calculated CRC            }
{         should be zero.                                                      }
{                                                                              }
{   This effectively means that using CRCs, it is possible to calculate a      }
{   series of bits to tag onto the data which makes the data an exact          }
{   multiple of the polynomial.                                                }
{                                                                              }
procedure CRC16Init(var CRC16: Word);
function CRC16Byte(const CRC16: Word; const Octet: Byte): Word;
function CRC16Buf(const CRC16: Word; const Buf; const BufSize: Integer): Word;

function CalcCRC16(const Buf; const BufSize: Integer): Word; overload;
function CalcCRC16(const Buf: AnsiString): Word; overload;

{                                                                              }
{ CRC 32 hashing                                                               }
{                                                                              }
procedure SetCRC32Poly(const Poly: LongWord);

procedure CRC32Init(var CRC32: LongWord);
function CRC32Byte(const CRC32: LongWord; const Octet: Byte): LongWord;
function CRC32Buf(const CRC32: LongWord; const Buf; const BufSize: Integer): LongWord;
function CRC32BufNoCase(const CRC32: LongWord; const Buf; const BufSize: Integer): LongWord;

function CalcCRC32(const Buf; const BufSize: Integer): LongWord; overload;
function CalcCRC32(const Buf: AnsiString): LongWord; overload;

{                                                                              }
{ Adler 32 hashing                                                             }
{                                                                              }
procedure Adler32Init(var Adler32: LongWord);
function Adler32Byte(const Adler32: LongWord; const Octet: Byte): LongWord;
function Adler32Buf(const Adler32: LongWord; const Buf; const BufSize: Integer): LongWord;

function CalcAdler32(const Buf; const BufSize: Integer): LongWord; overload;
function CalcAdler32(const Buf: AnsiString): LongWord; overload;

{                                                                              }
{ ELF hashing                                                                  }
{                                                                              }
procedure ELFInit(var Digest: LongWord);
function ELFBuf(const Digest: LongWord; const Buf; const BufSize: Integer): LongWord;

function CalcELF(const Buf; const BufSize: Integer): LongWord; overload;
function CalcELF(const Buf: AnsiString): LongWord; overload;

{                                                                              }
{ ISBN checksum                                                                }
{                                                                              }
function IsValidISBN(const S: AnsiString): Boolean;

{                                                                              }
{ LUHN checksum                                                                }
{                                                                              }
{   The LUHN forumula (also known as mod-10) is used in major credit card      }
{   account numbers for validity checking.                                     }
{                                                                              }
function IsValidLUHN(const S: AnsiString): Boolean;

{                                                                              }
{ Knuth hash                                                                   }
{ General purpose string hashing function proposed by Donald E Knuth in        }
{ 'The Art of Computer Programming Vol 3'.                                     }
{                                                                              }
function KnuthHashA(const S: AnsiString): LongWord;
function KnuthHashW(const S: WideString): LongWord;

{                                                                              }
{ MD5 hash                                                                     }
{                                                                              }
{   MD5 is an Internet standard secure hashing function, that was              }
{   developed by Professor Ronald L. Rivest in 1991. Subsequently it has       }
{   been placed in the public domain.                                          }
{   MD5 was developed to be more secure after MD4 was 'broken'.                }
{   Den Boer and Bosselaers estimate that if a custom machine were to be       }
{   built specifically to find collisions for MD5 (costing $10m in 1994) it    }
{   would on average take 24 days to find a collision.                         }
{                                                                              }
procedure MD5InitDigest(var Digest: T128BitDigest);
procedure MD5Buf(var Digest: T128BitDigest; const Buf; const BufSize: Integer);
procedure MD5FinalBuf(var Digest: T128BitDigest; const Buf; const BufSize: Integer;
  const TotalSize: Int64);

function CalcMD5(const Buf; const BufSize: Integer): T128BitDigest; overload;
function CalcMD5(const Buf: AnsiString): T128BitDigest; overload;

function MD5DigestToStrA(const Digest: T128BitDigest): AnsiString;
function MD5DigestToHexA(const Digest: T128BitDigest): AnsiString;
function MD5DigestToHexW(const Digest: T128BitDigest): WideString;

{                                                                              }
{ SHA1 Hashing                                                                 }
{                                                                              }
{   Specification at http://www.itl.nist.gov/fipspubs/fip180-1.htm             }
{   Also see RFC 3174.                                                         }
{   SHA1 was developed by NIST and is specified in the Secure Hash Standard    }
{   (SHS, FIPS 180) and corrects an unpublished flaw the original SHA          }
{   algorithm.                                                                 }
{   SHA1 produces a 160-bit digest and is considered more secure than MD5.     }
{   SHA1 has a similar design to the MD4-family of hash functions.             }
{                                                                              }
procedure SHA1InitDigest(var Digest: T160BitDigest);
procedure SHA1Buf(var Digest: T160BitDigest; const Buf; const BufSize: Integer);
procedure SHA1FinalBuf(var Digest: T160BitDigest; const Buf; const BufSize: Integer;
  const TotalSize: Int64);

function CalcSHA1(const Buf; const BufSize: Integer): T160BitDigest; overload;
function CalcSHA1(const Buf: AnsiString): T160BitDigest; overload;

function SHA1DigestToStrA(const Digest: T160BitDigest): AnsiString;
function SHA1DigestToHexA(const Digest: T160BitDigest): AnsiString;
function SHA1DigestToHexW(const Digest: T160BitDigest): WideString;

{                                                                              }
{ SHA224 Hashing                                                               }
{                                                                              }
{   224 bit SHA-2 hash                                                         }
{   http://en.wikipedia.org/wiki/SHA-2                                         }
{   SHA-224 is based on SHA-256                                                }
{                                                                              }
procedure SHA224InitDigest(var Digest: T256BitDigest);
procedure SHA224Buf(var Digest: T256BitDigest; const Buf; const BufSize: Integer);
procedure SHA224FinalBuf(var Digest: T256BitDigest; const Buf; const BufSize: Integer; const TotalSize: Int64;
  var OutDigest: T224BitDigest);

function CalcSHA224(const Buf; const BufSize: Integer): T224BitDigest; overload;
function CalcSHA224(const Buf: AnsiString): T224BitDigest; overload;

function SHA224DigestToStrA(const Digest: T224BitDigest): AnsiString;
function SHA224DigestToHexA(const Digest: T224BitDigest): AnsiString;
function SHA224DigestToHexW(const Digest: T224BitDigest): WideString;

{                                                                              }
{ SHA256 Hashing                                                               }
{   256 bit SHA-2 hash                                                         }
{                                                                              }
procedure SHA256InitDigest(var Digest: T256BitDigest);
procedure SHA256Buf(var Digest: T256BitDigest; const Buf; const BufSize: Integer);
procedure SHA256FinalBuf(var Digest: T256BitDigest; const Buf; const BufSize: Integer; const TotalSize: Int64);

function CalcSHA256(const Buf; const BufSize: Integer): T256BitDigest; overload;
function CalcSHA256(const Buf: AnsiString): T256BitDigest; overload;

function SHA256DigestToStrA(const Digest: T256BitDigest): AnsiString;
function SHA256DigestToHexA(const Digest: T256BitDigest): AnsiString;
function SHA256DigestToHexW(const Digest: T256BitDigest): WideString;

{                                                                              }
{ SHA384 Hashing                                                               }
{   384 bit SHA-2 hash                                                         }
{   SHA-384 is based on SHA-512                                                }
{                                                                              }
procedure SHA384InitDigest(var Digest: T512BitDigest);
procedure SHA384Buf(var Digest: T512BitDigest; const Buf; const BufSize: Integer);
procedure SHA384FinalBuf(var Digest: T512BitDigest; const Buf; const BufSize: Integer; const TotalSize: Int64; var OutDigest: T384BitDigest);

function CalcSHA384(const Buf; const BufSize: Integer): T384BitDigest; overload;
function CalcSHA384(const Buf: AnsiString): T384BitDigest; overload;

function SHA384DigestToStrA(const Digest: T384BitDigest): AnsiString;
function SHA384DigestToHexA(const Digest: T384BitDigest): AnsiString;
function SHA384DigestToHexW(const Digest: T384BitDigest): WideString;

{                                                                              }
{ SHA512 Hashing                                                               }
{   512 bit SHA-2 hash                                                         }
{                                                                              }
procedure SHA512InitDigest(var Digest: T512BitDigest);
procedure SHA512Buf(var Digest: T512BitDigest; const Buf; const BufSize: Integer);
procedure SHA512FinalBuf(var Digest: T512BitDigest; const Buf; const BufSize: Integer; const TotalSize: Int64);

function CalcSHA512(const Buf; const BufSize: Integer): T512BitDigest; overload;
function CalcSHA512(const Buf: AnsiString): T512BitDigest; overload;

function SHA512DigestToStrA(const Digest: T512BitDigest): AnsiString;
function SHA512DigestToHexA(const Digest: T512BitDigest): AnsiString;
function SHA512DigestToHexW(const Digest: T512BitDigest): WideString;

{                                                                              }
{ HMAC-MD5 keyed hashing                                                       }
{                                                                              }
{   HMAC allows secure keyed hashing (hashing with a password).                }
{   HMAC was designed to meet the requirements of the IPSEC working group in   }
{   the IETF, and is now a standard.                                           }
{   HMAC, are proven to be secure as long as the underlying hash function      }
{   has some reasonable cryptographic strengths.                               }
{   See RFC 2104 for details on HMAC.                                          }
{                                                                              }
procedure HMAC_MD5Init(const Key: Pointer; const KeySize: Integer;
  var Digest: T128BitDigest; var K: T512BitBuf);
procedure HMAC_MD5Buf(var Digest: T128BitDigest; const Buf; const BufSize: Integer);
procedure HMAC_MD5FinalBuf(const K: T512BitBuf; var Digest: T128BitDigest;
  const Buf; const BufSize: Integer; const TotalSize: Int64);

function CalcHMAC_MD5(const Key: Pointer; const KeySize: Integer;
  const Buf; const BufSize: Integer): T128BitDigest; overload;
function CalcHMAC_MD5(const Key: AnsiString; const Buf; const BufSize: Integer): T128BitDigest; overload;
function CalcHMAC_MD5(const Key, Buf: AnsiString): T128BitDigest; overload;

{                                                                              }
{ HMAC-SHA1 keyed hashing                                                      }
{                                                                              }
procedure HMAC_SHA1Init(const Key: Pointer; const KeySize: Integer;
  var Digest: T160BitDigest; var K: T512BitBuf);
procedure HMAC_SHA1Buf(var Digest: T160BitDigest; const Buf; const BufSize: Integer);
procedure HMAC_SHA1FinalBuf(const K: T512BitBuf; var Digest: T160BitDigest;
  const Buf; const BufSize: Integer; const TotalSize: Int64);

function CalcHMAC_SHA1(const Key: Pointer; const KeySize: Integer;
  const Buf; const BufSize: Integer): T160BitDigest; overload;
function CalcHMAC_SHA1(const Key: AnsiString; const Buf; const BufSize: Integer): T160BitDigest; overload;
function CalcHMAC_SHA1(const Key, Buf: AnsiString): T160BitDigest; overload;

{                                                                              }
{ HMAC-SHA256 keyed hashing                                                    }
{                                                                              }
procedure HMAC_SHA256Init(const Key: Pointer; const KeySize: Integer;
  var Digest: T256BitDigest; var K: T512BitBuf);
procedure HMAC_SHA256Buf(var Digest: T256BitDigest; const Buf; const BufSize: Integer);
procedure HMAC_SHA256FinalBuf(const K: T512BitBuf; var Digest: T256BitDigest;
  const Buf; const BufSize: Integer; const TotalSize: Int64);

function CalcHMAC_SHA256(const Key: Pointer; const KeySize: Integer;
  const Buf; const BufSize: Integer): T256BitDigest; overload;
function CalcHMAC_SHA256(const Key: AnsiString; const Buf; const BufSize: Integer): T256BitDigest; overload;
function CalcHMAC_SHA256(const Key, Buf: AnsiString): T256BitDigest; overload;

{                                                                              }
{ HMAC-SHA512 keyed hashing                                                    }
{                                                                              }
procedure HMAC_SHA512Init(const Key: Pointer; const KeySize: Integer; var Digest: T512BitDigest; var K: T1024BitBuf);
procedure HMAC_SHA512Buf(var Digest: T512BitDigest; const Buf; const BufSize: Integer);
procedure HMAC_SHA512FinalBuf(const K: T1024BitBuf; var Digest: T512BitDigest; const Buf; const BufSize: Integer; const TotalSize: Int64);

function CalcHMAC_SHA512(const Key: Pointer; const KeySize: Integer;
  const Buf; const BufSize: Integer): T512BitDigest; overload;
function CalcHMAC_SHA512(const Key: AnsiString; const Buf; const BufSize: Integer): T512BitDigest; overload;
function CalcHMAC_SHA512(const Key, Buf: AnsiString): T512BitDigest; overload;

{                                                                              }
{ Hash class wrappers                                                          }
{                                                                              }
type
  { AHash                                                                      }
  {   Base class for hash classes.                                             }
	AHash = class
	protected
		FDigest   : Pointer;
		FTotalSize: Int64;

		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); virtual; abstract;
		procedure ProcessBuf(const Buf; const BufSize: Integer); virtual; abstract;
		procedure ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64); virtual;

	public
		class function DigestSize: Integer; virtual; abstract;
		class function BlockSize: Integer; virtual;

		procedure Init(const Digest: Pointer; const Key: Pointer = nil;
		  const KeySize: Integer = 0); overload;
		procedure Init(const Digest: Pointer; const Key: AnsiString = ''); overload;

		procedure HashBuf(const Buf; const BufSize: Integer; const FinalBuf: Boolean);
		procedure HashFile(const FileName: string; const Offset: Int64 = 0;
		  const MaxCount: Int64 = - 1);
	end;

	THashClass = class of AHash;

  { TChecksum32Hash                                                            }
	TChecksum32Hash = class(AHash)
	protected
		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); override;
		procedure ProcessBuf(const Buf; const BufSize: Integer); override;

	public
		class function DigestSize: Integer; override;
	end;

  { TXOR8Hash                                                                  }
	TXOR8Hash = class(AHash)
	protected
		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); override;
		procedure ProcessBuf(const Buf; const BufSize: Integer); override;

	public
		class function DigestSize: Integer; override;
	end;

  { TXOR16Hash                                                                 }
	TXOR16Hash = class(AHash)
	protected
		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); override;
		procedure ProcessBuf(const Buf; const BufSize: Integer); override;

	public
		class function DigestSize: Integer; override;
	end;

  { TXOR32Hash                                                                 }
	TXOR32Hash = class(AHash)
	protected
		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); override;
		procedure ProcessBuf(const Buf; const BufSize: Integer); override;

	public
		class function DigestSize: Integer; override;
	end;

  { TCRC16Hash                                                                 }
	TCRC16Hash = class(AHash)
	protected
		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); override;
		procedure ProcessBuf(const Buf; const BufSize: Integer); override;

	public
		class function DigestSize: Integer; override;
	end;

  { TCRC32Hash                                                                 }
	TCRC32Hash = class(AHash)
	protected
		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); override;
		procedure ProcessBuf(const Buf; const BufSize: Integer); override;

	public
		class function DigestSize: Integer; override;
	end;

  { TAdler32Hash                                                               }
	TAdler32Hash = class(AHash)
	protected
		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); override;
		procedure ProcessBuf(const Buf; const BufSize: Integer); override;

	public
		class function DigestSize: Integer; override;
	end;

  { TELFHash                                                                   }
	TELFHash = class(AHash)
	protected
		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); override;
		procedure ProcessBuf(const Buf; const BufSize: Integer); override;

	public
		class function DigestSize: Integer; override;
	end;

  { TMD5Hash                                                                   }
	TMD5Hash = class(AHash)
	protected
		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); override;
		procedure ProcessBuf(const Buf; const BufSize: Integer); override;
		procedure ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64); override;

	public
		class function DigestSize: Integer; override;
		class function BlockSize: Integer; override;
	end;

  { TSHA1Hash                                                                  }
	TSHA1Hash = class(AHash)
	protected
		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); override;
		procedure ProcessBuf(const Buf; const BufSize: Integer); override;
		procedure ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64); override;

	public
		class function DigestSize: Integer; override;
		class function BlockSize: Integer; override;
	end;

  { TSHA256Hash                                                                }
	TSHA256Hash = class(AHash)
	protected
		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); override;
		procedure ProcessBuf(const Buf; const BufSize: Integer); override;
		procedure ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64); override;

	public
		class function DigestSize: Integer; override;
		class function BlockSize: Integer; override;
	end;

  { TSHA512Hash                                                                }
	TSHA512Hash = class(AHash)
	protected
		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); override;
		procedure ProcessBuf(const Buf; const BufSize: Integer); override;
		procedure ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64); override;

	public
		class function DigestSize: Integer; override;
		class function BlockSize: Integer; override;
	end;

  { THMAC_MD5Hash                                                              }
	THMAC_MD5Hash = class(AHash)
	protected
		FKey: T512BitBuf;

		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); override;
		procedure ProcessBuf(const Buf; const BufSize: Integer); override;
		procedure ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64); override;

	public
		class function DigestSize: Integer; override;
		class function BlockSize: Integer; override;

		destructor Destroy; override;
	end;

  { THMAC_SHA1Hash                                                             }
	THMAC_SHA1Hash = class(AHash)
	protected
		FKey: T512BitBuf;

		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); override;
		procedure ProcessBuf(const Buf; const BufSize: Integer); override;
		procedure ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64); override;

	public
		class function DigestSize: Integer; override;
		class function BlockSize: Integer; override;

		destructor Destroy; override;
	end;

  { THMAC_SHA256Hash                                                           }
	THMAC_SHA256Hash = class(AHash)
	protected
		FKey: T512BitBuf;

		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); override;
		procedure ProcessBuf(const Buf; const BufSize: Integer); override;
		procedure ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64); override;

	public
		class function DigestSize: Integer; override;
		class function BlockSize: Integer; override;

		destructor Destroy; override;
	end;

  { THMAC_SHA512Hash                                                           }
	THMAC_SHA512Hash = class(AHash)
	protected
		FKey: T1024BitBuf;

		procedure InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer); override;
		procedure ProcessBuf(const Buf; const BufSize: Integer); override;
		procedure ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64); override;

	public
		class function DigestSize: Integer; override;
		class function BlockSize: Integer; override;

		destructor Destroy; override;
	end;

{                                                                              }
{ THashType                                                                    }
{                                                                              }
type
	THashType = (
	  hashChecksum32, hashXOR8, hashXOR16, hashXOR32,
	  hashCRC16, hashCRC32,
	  hashAdler32,
	  hashELF,
	  hashMD5, hashSHA1, hashSHA256, hashSHA512,
	  hashHMAC_MD5, hashHMAC_SHA1, hashHMAC_SHA256, hashHMAC_SHA512);

{                                                                              }
{ GetHashClassByType                                                           }
{                                                                              }
function GetHashClassByType(const HashType: THashType): THashClass;
function GetDigestSize(const HashType: THashType): Integer;

{                                                                              }
{ CalculateHash                                                                }
{                                                                              }
procedure CalculateHash(const HashType: THashType;
  const Buf; const BufSize: Integer; const Digest: Pointer;
  const Key: Pointer = nil; const KeySize: Integer = 0); overload;
procedure CalculateHash(const HashType: THashType;
  const Buf; const BufSize: Integer;
  const Digest: Pointer; const Key: AnsiString = ''); overload;
procedure CalculateHash(const HashType: THashType;
  const Buf: AnsiString; const Digest: Pointer;
  const Key: AnsiString = ''); overload;

{                                                                              }
{ HashString                                                                   }
{                                                                              }
{   HashString is a fast general purpose ASCII string hashing function.        }
{   It returns a 32 bit value in the range 0 to Slots - 1. If Slots = 0 then   }
{   the full 32 bit value is returned.                                         }
{   If CaseSensitive = False then HashString will return the same hash value   }
{   regardless of the case of the characters in the string.                    }
{                                                                              }
{   The implementation is based on CRC32. It uses up to 48 characters from     }
{   the string (first 16 characters, last 16 characters and 16 characters      }
{   uniformly sampled from the remaining characters) to calculate the hash     }
{   value.                                                                     }
{                                                                              }
function HashString(const StrBuf: Pointer; const StrLength: Integer;
  const Slots: LongWord = 0; const CaseSensitive: Boolean = True): LongWord; overload;
function HashString(const S: AnsiString; const Slots: LongWord = 0;
  const CaseSensitive: Boolean = True): LongWord; overload;

implementation

{                                                                              }
{ Hash errors                                                                  }
{                                                                              }
const
	hashErrorMessages: array [0 .. hashMAX_ERROR] of string = (
	  '',
	  'Internal error',
	  'Invalid hash type',
	  'Invalid buffer',
	  'Invalid buffer size',
	  'Invalid digest',
	  'Invalid key',
	  'Invalid file name',
	  'File open error',
	  'File seek error',
	  'File read error',
	  'Not a keyed hash type',
	  'Too many open handles',
	  'Invalid handle');

function GetHashErrorMessage(const ErrorCode: LongWord): PChar;
begin
	if (ErrorCode = hashNoError) or (ErrorCode > hashMAX_ERROR) then
		Result := nil
	else
		Result := PChar(hashErrorMessages[ErrorCode]);
end;

{                                                                              }
{ EHashError                                                                   }
{                                                                              }
constructor EHashError.Create(const ErrorCode: LongWord; const Msg: string);
begin
	FErrorCode := ErrorCode;
	if (Msg = '') and (ErrorCode <= hashMAX_ERROR) then
		inherited Create(hashErrorMessages[ErrorCode])
	else
		inherited Create(Msg);
end;

{                                                                              }
{ Secure memory clear                                                          }
{                                                                              }
procedure SecureClear(var Buf; const BufSize: Integer);
begin
	if BufSize <= 0 then
		exit;
	FillChar(Buf, BufSize, #$00);
end;

procedure SecureClear512(var Buf: T512BitBuf);
begin
	SecureClear(Buf, SizeOf(Buf));
end;

procedure SecureClear1024(var Buf: T1024BitBuf);
begin
	SecureClear(Buf, SizeOf(Buf));
end;

procedure SecureClearStrA(var S: AnsiString);
var
	L: Integer;
begin
	L := Length(S);
	if L = 0 then
		exit;
	SecureClear(S[1], L);
	S := '';
end;

procedure SecureClearStrW(var S: WideString);
var
	L: Integer;
begin
	L := Length(S);
	if L = 0 then
		exit;
	SecureClear(S[1], L * SizeOf(WideChar));
	S := '';
end;

{                                                                              }
{ Checksum hashing                                                             }
{                                                                              }
{$IFDEF ASM386_DELPHI}


function CalcChecksum32(const Buf; const BufSize: Integer): LongWord;
asm
	or eax, eax              //eax = Buf
	jz @fin
	or edx, edx              //edx = BufSize
	jbe @finz
	push esi
	mov esi, eax
	add esi, edx
	xor eax, eax
	xor ecx, ecx
@l1:
	dec esi
	mov cl, [esi]
	add eax, ecx
	dec edx
	jnz @l1
	pop esi
@fin:
	ret
@finz:
	xor eax, eax
end;
{$ELSE}


function CalcChecksum32(const Buf; const BufSize: Integer): LongWord;
var
	I: Integer;
	P: PByte;
begin
	Result := 0;
	P      := @Buf;
	for I  := 1 to BufSize do
	begin
		Inc(Result, P^);
		Inc(P);
	end;
end;
{$ENDIF}


function CalcChecksum32(const Buf: AnsiString): LongWord;
begin
	Result := CalcChecksum32(Pointer(Buf)^, Length(Buf));
end;

{                                                                              }
{ XOR hashing                                                                  }
{                                                                              }
{$IFDEF ASM386_DELPHI}


function XOR32Buf(const Buf; const BufSize: Integer): LongWord;
Asm
	or eax, eax
	jz @fin
	or edx, edx
	jz @finz

	push esi
	mov esi, eax
	xor eax, eax

	mov ecx, edx
	shr ecx, 2
	jz @rest

@l1:
	xor eax, [esi]
	add esi, 4
	dec ecx
	jnz @l1

@rest:
	and edx, 3
	jz @finp
	xor al, [esi]
	dec edx
	jz @finp
	inc esi
	xor ah, [esi]
	dec edx
	jz @finp
	inc esi
	mov dl, [esi]
	shl edx, 16
	xor eax, edx

@finp:
	pop esi
	ret
@finz:
	xor eax, eax
@fin:
	ret
end;
{$ELSE}


function XOR32Buf(const Buf; const BufSize: Integer): LongWord;
var
	I: Integer;
	L: Byte;
	P: PAnsiChar;
begin
	Result := 0;
	L      := 0;
	P      := @Buf;
	for I  := 1 to BufSize do
	begin
		Result := Result xor (Byte(P^) shl L);
		Inc(L, 8);
		if L = 32 then
			L := 0;
		Inc(P);
	end;
end;
{$ENDIF}


function CalcXOR8(const Buf; const BufSize: Integer): Byte;
var
	L: LongWord;
begin
	L      := XOR32Buf(Buf, BufSize);
	Result := Byte(L) xor
	  Byte(L shr 8) xor
	  Byte(L shr 16) xor
	  Byte(L shr 24);
end;

function CalcXOR8(const Buf: AnsiString): Byte;
begin
	Result := CalcXOR8(Pointer(Buf)^, Length(Buf));
end;

function CalcXOR16(const Buf; const BufSize: Integer): Word;
var
	L: LongWord;
begin
	L      := XOR32Buf(Buf, BufSize);
	Result := Word(L) xor
	  Word(L shr 16);
end;

function CalcXOR16(const Buf: AnsiString): Word;
begin
	Result := CalcXOR16(Pointer(Buf)^, Length(Buf));
end;

function CalcXOR32(const Buf; const BufSize: Integer): LongWord;
begin
	Result := XOR32Buf(Buf, BufSize);
end;

function CalcXOR32(const Buf: AnsiString): LongWord;
begin
	Result := XOR32Buf(Pointer(Buf)^, Length(Buf));
end;

{                                                                              }
{ CRC 16 hashing                                                               }
{                                                                              }
const
	CRC16Table: array [Byte] of Word = (
	  $0000, $1021, $2042, $3063, $4084, $50A5, $60C6, $70E7,
	  $8108, $9129, $A14A, $B16B, $C18C, $D1AD, $E1CE, $F1EF,
	  $1231, $0210, $3273, $2252, $52B5, $4294, $72F7, $62D6,
	  $9339, $8318, $B37B, $A35A, $D3BD, $C39C, $F3FF, $E3DE,
	  $2462, $3443, $0420, $1401, $64E6, $74C7, $44A4, $5485,
	  $A56A, $B54B, $8528, $9509, $E5EE, $F5CF, $C5AC, $D58D,
	  $3653, $2672, $1611, $0630, $76D7, $66F6, $5695, $46B4,
	  $B75B, $A77A, $9719, $8738, $F7DF, $E7FE, $D79D, $C7BC,
	  $48C4, $58E5, $6886, $78A7, $0840, $1861, $2802, $3823,
	  $C9CC, $D9ED, $E98E, $F9AF, $8948, $9969, $A90A, $B92B,
	  $5AF5, $4AD4, $7AB7, $6A96, $1A71, $0A50, $3A33, $2A12,
	  $DBFD, $CBDC, $FBBF, $EB9E, $9B79, $8B58, $BB3B, $AB1A,
	  $6CA6, $7C87, $4CE4, $5CC5, $2C22, $3C03, $0C60, $1C41,
	  $EDAE, $FD8F, $CDEC, $DDCD, $AD2A, $BD0B, $8D68, $9D49,
	  $7E97, $6EB6, $5ED5, $4EF4, $3E13, $2E32, $1E51, $0E70,
	  $FF9F, $EFBE, $DFDD, $CFFC, $BF1B, $AF3A, $9F59, $8F78,
	  $9188, $81A9, $B1CA, $A1EB, $D10C, $C12D, $F14E, $E16F,
	  $1080, $00A1, $30C2, $20E3, $5004, $4025, $7046, $6067,
	  $83B9, $9398, $A3FB, $B3DA, $C33D, $D31C, $E37F, $F35E,
	  $02B1, $1290, $22F3, $32D2, $4235, $5214, $6277, $7256,
	  $B5EA, $A5CB, $95A8, $8589, $F56E, $E54F, $D52C, $C50D,
	  $34E2, $24C3, $14A0, $0481, $7466, $6447, $5424, $4405,
	  $A7DB, $B7FA, $8799, $97B8, $E75F, $F77E, $C71D, $D73C,
	  $26D3, $36F2, $0691, $16B0, $6657, $7676, $4615, $5634,
	  $D94C, $C96D, $F90E, $E92F, $99C8, $89E9, $B98A, $A9AB,
	  $5844, $4865, $7806, $6827, $18C0, $08E1, $3882, $28A3,
	  $CB7D, $DB5C, $EB3F, $FB1E, $8BF9, $9BD8, $ABBB, $BB9A,
	  $4A75, $5A54, $6A37, $7A16, $0AF1, $1AD0, $2AB3, $3A92,
	  $FD2E, $ED0F, $DD6C, $CD4D, $BDAA, $AD8B, $9DE8, $8DC9,
	  $7C26, $6C07, $5C64, $4C45, $3CA2, $2C83, $1CE0, $0CC1,
	  $EF1F, $FF3E, $CF5D, $DF7C, $AF9B, $BFBA, $8FD9, $9FF8,
	  $6E17, $7E36, $4E55, $5E74, $2E93, $3EB2, $0ED1, $1EF0);

function CRC16Byte(const CRC16: Word; const Octet: Byte): Word;
begin
	Result := CRC16Table[Byte(Hi(CRC16) xor Octet)] xor Word(CRC16 shl 8);
end;

function CRC16Buf(const CRC16: Word; const Buf; const BufSize: Integer): Word;
var
	I: Integer;
	P: PByte;
begin
	Result := CRC16;
	P      := @Buf;
	for I  := 1 to BufSize do
	begin
		Result := CRC16Byte(Result, P^);
		Inc(P);
	end;
end;

procedure CRC16Init(var CRC16: Word);
begin
	CRC16 := $FFFF;
end;

function CalcCRC16(const Buf; const BufSize: Integer): Word;
begin
	CRC16Init(Result);
	Result := CRC16Buf(Result, Buf, BufSize);
end;

function CalcCRC16(const Buf: AnsiString): Word;
begin
	Result := CalcCRC16(Pointer(Buf)^, Length(Buf));
end;

{                                                                              }
{ CRC 32 hashing                                                               }
{                                                                              }
var
	CRC32TableInit: Boolean = False;
	CRC32Table    : array [Byte] of LongWord;
	CRC32Poly     : LongWord = $EDB88320;

procedure InitCRC32Table;
var
	I, J: Byte;
	R   : LongWord;
begin
	for I := $00 to $FF do
	begin
		R     := I;
		for J := 8 downto 1 do
			if R and 1 <> 0 then
				R := (R shr 1) xor CRC32Poly
			else
				R     := R shr 1;
		CRC32Table[I] := R;
	end;
	CRC32TableInit := True;
end;

procedure SetCRC32Poly(const Poly: LongWord);
begin
	CRC32Poly      := Poly;
	CRC32TableInit := False;
end;

function CalcCRC32Byte(const CRC32: LongWord; const Octet: Byte): LongWord; {$IFDEF UseInline}inline; {$ENDIF}
begin
	Result := CRC32Table[Byte(CRC32) xor Octet] xor ((CRC32 shr 8) and $00FFFFFF);
end;

function CRC32Byte(const CRC32: LongWord; const Octet: Byte): LongWord;
begin
	if not CRC32TableInit then
		InitCRC32Table;
	Result := CalcCRC32Byte(CRC32, Octet);
end;

function CRC32Buf(const CRC32: LongWord; const Buf; const BufSize: Integer): LongWord;
var
	P: PByte;
	I: Integer;
begin
	if not CRC32TableInit then
		InitCRC32Table;
	P      := @Buf;
	Result := CRC32;
	for I  := 1 to BufSize do
	begin
		Result := CalcCRC32Byte(Result, P^);
		Inc(P);
	end;
end;

function CRC32BufNoCase(const CRC32: LongWord; const Buf; const BufSize: Integer): LongWord;
var
	P: PByte;
	I: Integer;
	C: Byte;
begin
	if not CRC32TableInit then
		InitCRC32Table;
	P      := @Buf;
	Result := CRC32;
	for I  := 1 to BufSize do
	begin
		C := P^;
		if AnsiChar(C) in ['A' .. 'Z'] then
			C  := C or 32;
		Result := CalcCRC32Byte(Result, C);
		Inc(P);
	end;
end;

procedure CRC32Init(var CRC32: LongWord);
begin
	CRC32 := $FFFFFFFF;
end;

function CalcCRC32(const Buf; const BufSize: Integer): LongWord;
begin
	CRC32Init(Result);
	Result := not CRC32Buf(Result, Buf, BufSize);
end;

function CalcCRC32(const Buf: AnsiString): LongWord;
begin
	Result := CalcCRC32(Pointer(Buf)^, Length(Buf));
end;

{                                                                              }
{ Adler 32 hashing                                                             }
{                                                                              }
procedure Adler32Init(var Adler32: LongWord);
begin
	Adler32 := $00000001;
end;

const
	Adler32Mod = 65521; // largest prime smaller than 65536

function Adler32Byte(const Adler32: LongWord; const Octet: Byte): LongWord;
var
	A, B: LongWord;
begin
	A := Adler32 and $0000FFFF;
	B := Adler32 shr 16;
	Inc(A, Octet);
	Inc(B, A);
	if A >= Adler32Mod then
		Dec(A, Adler32Mod);
	if B >= Adler32Mod then
		Dec(B, Adler32Mod);
	Result := A or (B shl 16);
end;

function Adler32Buf(const Adler32: LongWord; const Buf; const BufSize: Integer): LongWord;
var
	A, B: LongWord;
	P   : PByte;
	I   : Integer;
begin
	A     := Adler32 and $0000FFFF;
	B     := Adler32 shr 16;
	P     := @Buf;
	for I := 1 to BufSize do
	begin
		Inc(A, P^);
		Inc(B, A);
		if A >= Adler32Mod then
			Dec(A, Adler32Mod);
		if B >= Adler32Mod then
			Dec(B, Adler32Mod);
		Inc(P);
	end;
	Result := A or (B shl 16);
end;

function CalcAdler32(const Buf; const BufSize: Integer): LongWord;
begin
	Adler32Init(Result);
	Result := Adler32Buf(Result, Buf, BufSize);
end;

function CalcAdler32(const Buf: AnsiString): LongWord;
begin
	Result := CalcAdler32(Pointer(Buf)^, Length(Buf));
end;

{                                                                              }
{ ELF hashing                                                                  }
{                                                                              }
procedure ELFInit(var Digest: LongWord);
begin
	Digest := 0;
end;

function ELFBuf(const Digest: LongWord; const Buf; const BufSize: Integer): LongWord;
var
	I: Integer;
	P: PByte;
	X: LongWord;
begin
	Result := Digest;
	P      := @Buf;
	for I  := 1 to BufSize do
	begin
		Result := (Result shl 4) + P^;
		Inc(P);
		X := Result and $F0000000;
		if X <> 0 then
			Result := Result xor (X shr 24);
		Result     := Result and (not X);
	end;
end;

function CalcELF(const Buf; const BufSize: Integer): LongWord;
begin
	Result := ELFBuf(0, Buf, BufSize);
end;

function CalcELF(const Buf: AnsiString): LongWord;
begin
	Result := CalcELF(Pointer(Buf)^, Length(Buf));
end;

{                                                                              }
{ ISBN checksum                                                                }
{                                                                              }
function IsValidISBN(const S: AnsiString): Boolean;
var
	I, L, M, D, C: Integer;
	P            : PAnsiChar;
begin
	L := Length(S);
	if L < 10 then // too few digits
	begin
		Result := False;
		exit;
	end;
	M     := 10;
	C     := 0;
	P     := Pointer(S);
	for I := 1 to L do
	begin
		if (P^ in ['0' .. '9']) or ((M = 1) and (P^ in ['x', 'X'])) then
		begin
			if M = 0 then // too many digits
			begin
				Result := False;
				exit;
			end;
			if P^ in ['x', 'X'] then
				D := 10
			else
				D := Ord(P^) - Ord('0');
			Inc(C, M * D);
			Dec(M);
		end;
		Inc(P);
	end;
	if M > 0 then // too few digits
	begin
		Result := False;
		exit;
	end;
	Result := C mod 11 = 0;
end;

{                                                                              }
{ LUHN checksum                                                                }
{                                                                              }
function IsValidLUHN(const S: AnsiString): Boolean;
var
	P            : PAnsiChar;
	I, L, M, C, D: Integer;
	R            : Boolean;
begin
	L := Length(S);
	if L = 0 then
	begin
		Result := False;
		exit;
	end;
	P := Pointer(S);
	Inc(P, L - 1);
	C     := 0;
	M     := 0;
	R     := False;
	for I := 1 to L do
	begin
		if P^ in ['0' .. '9'] then
		begin
			D := Ord(P^) - Ord('0');
			if R then
			begin
				D := D * 2;
				D := (D div 10) + (D mod 10);
			end;
			Inc(C, D);
			Inc(M);
			R := not R;
		end;
		Dec(P);
	end;
	Result := (M >= 1) and (C mod 10 = 0);
end;

{                                                                              }
{ Knuth Hash                                                                   }
{                                                                              }
function KnuthHashA(const S: AnsiString): LongWord;
var
	I, L: Integer;
	H   : LongWord;
begin
	L      := Length(S);
	H      := L;
	for I  := 1 to L do
		H  := ((H shr 5) xor (H shl 27)) xor Ord(S[I]);
	Result := H;
end;

function KnuthHashW(const S: WideString): LongWord;
var
	I, L: Integer;
	H   : LongWord;
begin
	L      := Length(S);
	H      := L;
	for I  := 1 to L do
		H  := ((H shr 5) xor (H shl 27)) xor Ord(S[I]);
	Result := H;
end;

{                                                                              }
{ Digests                                                                      }
{                                                                              }
const
	s_HexDigitsLower: string[16] = '0123456789abcdef';

procedure DigestToHexBufA(const Digest; const Size: Integer; const Buf);
var
	I: Integer;
	P: PAnsiChar;
	Q: PByte;
begin
	P := @Buf;;
	Assert(Assigned(P));
	Q := @Digest;
	Assert(Assigned(Q));
	for I := 0 to Size - 1 do
	begin
		P^ := s_HexDigitsLower[Q^ shr 4 + 1];
		Inc(P);
		P^ := s_HexDigitsLower[Q^ and 15 + 1];
		Inc(P);
		Inc(Q);
	end;
end;

procedure DigestToHexBufW(const Digest; const Size: Integer; const Buf);
var
	I: Integer;
	P: PWideChar;
	Q: PByte;
begin
	P := @Buf;;
	Assert(Assigned(P));
	Q := @Digest;
	Assert(Assigned(Q));
	for I := 0 to Size - 1 do
	begin
		P^ := WideChar(s_HexDigitsLower[Q^ shr 4 + 1]);
		Inc(P);
		P^ := WideChar(s_HexDigitsLower[Q^ and 15 + 1]);
		Inc(P);
		Inc(Q);
	end;
end;

function DigestToHexA(const Digest; const Size: Integer): AnsiString;
begin
	SetLength(Result, Size * 2);
	DigestToHexBufA(Digest, Size, Pointer(Result)^);
end;

function DigestToHexW(const Digest; const Size: Integer): WideString;
begin
	SetLength(Result, Size * 2);
	DigestToHexBufW(Digest, Size, Pointer(Result)^);
end;

function Digest128Equal(const Digest1, Digest2: T128BitDigest): Boolean;
var
	I: Integer;
begin
	for I := 0 to 3 do
		if Digest1.Longs[I] <> Digest2.Longs[I] then
		begin
			Result := False;
			exit;
		end;
	Result := True;
end;

function Digest160Equal(const Digest1, Digest2: T160BitDigest): Boolean;
var
	I: Integer;
begin
	for I := 0 to 4 do
		if Digest1.Longs[I] <> Digest2.Longs[I] then
		begin
			Result := False;
			exit;
		end;
	Result := True;
end;

function Digest224Equal(const Digest1, Digest2: T224BitDigest): Boolean;
var
	I: Integer;
begin
	for I := 0 to 6 do
		if Digest1.Longs[I] <> Digest2.Longs[I] then
		begin
			Result := False;
			exit;
		end;
	Result := True;
end;

function Digest256Equal(const Digest1, Digest2: T256BitDigest): Boolean;
var
	I: Integer;
begin
	for I := 0 to 7 do
		if Digest1.Longs[I] <> Digest2.Longs[I] then
		begin
			Result := False;
			exit;
		end;
	Result := True;
end;

function Digest384Equal(const Digest1, Digest2: T384BitDigest): Boolean;
var
	I: Integer;
begin
	for I := 0 to 11 do
		if Digest1.Longs[I] <> Digest2.Longs[I] then
		begin
			Result := False;
			exit;
		end;
	Result := True;
end;

function Digest512Equal(const Digest1, Digest2: T512BitDigest): Boolean;
var
	I: Integer;
begin
	for I := 0 to 15 do
		if Digest1.Longs[I] <> Digest2.Longs[I] then
		begin
			Result := False;
			exit;
		end;
	Result := True;
end;

{                                                                              }
{ ReverseMem                                                                   }
{ Utility function to reverse order of data in buffer.                         }
{                                                                              }
procedure ReverseMem(var Buf; const BufSize: Integer);
var
	I: Integer;
	P: PByte;
	Q: PByte;
	T: Byte;
begin
	P := @Buf;
	Q := P;
	Inc(Q, BufSize - 1);
	for I := 1 to BufSize div 2 do
	begin
		T  := P^;
		P^ := Q^;
		Q^ := T;
		Inc(P);
		Dec(Q);
	end;
end;

{                                                                              }
{ StdFinalBuf                                                                  }
{ Utility function to prepare final buffer(s).                                 }
{ Fills Buf1 and potentially Buf2 from Buf (FinalBufCount = 1 or 2).           }
{ Used by MD5, SHA1, SHA256, SHA512.                                           }
{                                                                              }
procedure StdFinalBuf512(
  const Buf; const BufSize: Integer; const TotalSize: Int64;
  var Buf1, Buf2: T512BitBuf;
  var FinalBufs: Integer;
  const SwapEndian: Boolean);
var
	P, Q: PByte;
	I   : Integer;
	L   : Int64;
begin
	Assert(BufSize < 64, 'Final BufSize must be less than 64 bytes');
	Assert(TotalSize >= BufSize, 'TotalSize >= BufSize');

	P := @Buf;
	Q := @Buf1[0];
	if BufSize > 0 then
	begin
		Move(P^, Q^, BufSize);
		Inc(Q, BufSize);
	end;
	Q^ := $80;
	Inc(Q);

{$IFDEF DELPHI5}
  // Delphi 5 sometimes reports fatal error (internal error C1093) when compiling:
  //   L := TotalSize * 8
	L := TotalSize;
	L := L * 8;
{$ELSE}
	L := TotalSize * 8;
{$ENDIF}
	if SwapEndian then
		ReverseMem(L, 8);
	if BufSize + 1 > 64 - Sizeof(Int64) then
	begin
		FillChar(Q^, 64 - BufSize - 1, #0);
		Q := @Buf2[0];
		FillChar(Q^, 64 - Sizeof(Int64), #0);
		Inc(Q, 64 - Sizeof(Int64));
		PInt64(Q)^ := L;
		FinalBufs  := 2;
	end
	else
	begin
		I := 64 - Sizeof(Int64) - BufSize - 1;
		FillChar(Q^, I, #0);
		Inc(Q, I);
		PInt64(Q)^ := L;
		FinalBufs  := 1;
	end;
end;

procedure StdFinalBuf1024(
  const Buf; const BufSize: Integer; const TotalSize: Int64;
  var Buf1, Buf2: T1024BitBuf;
  var FinalBufs: Integer;
  const SwapEndian: Boolean);
var
	P, Q: PByte;
	I   : Integer;
	L   : Int64;
begin
	Assert(BufSize < 128, 'Final BufSize must be less than 128 bytes');
	Assert(TotalSize >= BufSize, 'TotalSize >= BufSize');

	P := @Buf;
	Q := @Buf1[0];
	if BufSize > 0 then
	begin
		Move(P^, Q^, BufSize);
		Inc(Q, BufSize);
	end;
	Q^ := $80;
	Inc(Q);

{$IFDEF DELPHI5}
  // Delphi 5 sometimes reports fatal error (internal error C1093) when compiling:
  //   L := TotalSize * 8
	L := TotalSize;
	L := L * 8;
{$ELSE}
	L := TotalSize * 8;
{$ENDIF}
	if SwapEndian then
		ReverseMem(L, 8);
	if BufSize + 1 > 128 - Sizeof(Int64) * 2 then
	begin
		FillChar(Q^, 128 - BufSize - 1, #0);
		Q := @Buf2[0];
		FillChar(Q^, 128 - Sizeof(Int64) * 2, #0);
		Inc(Q, 128 - Sizeof(Int64) * 2);
		PInt64(Q)^ := 0;
		Inc(Q, 8);
		PInt64(Q)^ := L;
		FinalBufs  := 2;
	end
	else
	begin
		I := 128 - Sizeof(Int64) * 2 - BufSize - 1;
		FillChar(Q^, I, #0);
		Inc(Q, I);
		PInt64(Q)^ := 0;
		Inc(Q, 8);
		PInt64(Q)^ := L;
		FinalBufs  := 1;
	end;
end;

{                                                                              }
{ Utility functions SwapEndian, RotateLeftBits, RotateRightBits.               }
{ Used by SHA1 and SHA256.                                                     }
{                                                                              }
{$IFDEF ASM386}


function SwapEndian(const Value: LongWord): LongWord; register; assembler;
asm
	XCHG    AH, AL
	ROL     EAX, 16
	XCHG    AH, AL
end;
{$ELSE}


function SwapEndian(const Value: LongWord): LongWord;
begin
	Result := ((Value and $000000FF) shl 24) or
	  ((Value and $0000FF00) shl 8) or
	  ((Value and $00FF0000) shr 8) or
	  ((Value and $FF000000) shr 24);
end;
{$ENDIF}


procedure SwapEndianBuf(var Buf; const Count: Integer);
var
	P: PLongWord;
	I: Integer;
begin
	P     := @Buf;
	for I := 1 to Count do
	begin
		P^ := SwapEndian(P^);
		Inc(P);
	end;
end;

{$IFDEF ASM386_DELPHI}


function RotateLeftBits(const Value: LongWord; const Bits: Byte): LongWord;
asm
	MOV     CL, DL
	ROL     EAX, CL
end;
{$ELSE}


function RotateLeftBits(const Value: LongWord; const Bits: Byte): LongWord;
var
	I: Integer;
begin
	Result := Value;
	for I  := 1 to Bits do
		if Result and $80000000 = 0 then
			Result := Value shl 1
		else
			Result := (Value shl 1) or 1;
end;
{$ENDIF}

{$IFDEF ASM386_DELPHI}


function RotateRightBits(const Value: LongWord; const Bits: Byte): LongWord;
asm
	MOV     CL, DL
	ROR     EAX, CL
end;
{$ELSE}


function RotateRightBits(const Value: LongWord; const Bits: Byte): LongWord;
var
	I, B: Integer;
begin
	Result := Value;
	if Bits >= 32 then
		B := Bits mod 32
	else
		B := Bits;
	for I := 1 to B do
		if Result and 1 = 0 then
			Result := Result shr 1
		else
			Result := (Result shr 1) or $80000000;
end;
{$ENDIF}


{                                                                              }
{ Utility functions for Word64 arithmetic                                      }
{ Used by SHA-512                                                              }
{                                                                              }
procedure Word64InitZero(var A: Word64);
begin
	A.LongWords[0] := 0;
	A.LongWords[1] := 0;
end;

procedure Word64Not(var A: Word64);
begin
	A.LongWords[0] := not A.LongWords[0];
	A.LongWords[1] := not A.LongWords[1];
end;

procedure Word64AndWord64(var A: Word64; const B: Word64);
begin
	A.LongWords[0] := A.LongWords[0] and B.LongWords[0];
	A.LongWords[1] := A.LongWords[1] and B.LongWords[1];
end;

procedure Word64XorWord64(var A: Word64; const B: Word64);
begin
	A.LongWords[0] := A.LongWords[0] xor B.LongWords[0];
	A.LongWords[1] := A.LongWords[1] xor B.LongWords[1];
end;

procedure Word64AddWord64(var A: Word64; const B: Word64);
var
	C, D: Int64;
begin
	C := Int64(A.LongWords[0]) + B.LongWords[0];
	D := Int64(A.LongWords[1]) + B.LongWords[1];
	if C >= $100000000 then
		Inc(D);
	A.LongWords[0] := C and $FFFFFFFF;
	A.LongWords[1] := D and $FFFFFFFF;
end;

procedure Word64Shr(var A: Word64; const B: Byte);
var
	C: Byte;
begin
	if B = 0 then
		exit;
	if B >= 64 then
		Word64InitZero(A)
	else
	  if B < 32 then
	begin
		C              := 32 - B;
		A.LongWords[0] := (A.LongWords[0] shr B) or (A.LongWords[1] shl C);
		A.LongWords[1] := A.LongWords[1] shr B;
	end
	else
	begin
		C              := B - 32;
		A.LongWords[0] := A.LongWords[1] shr C;
		A.LongWords[1] := 0;
	end;
end;

procedure Word64Ror(var A: Word64; const B: Byte);
var
	C, D: Byte;
	E, F: LongWord;
begin
	C := B mod 64;
	if C = 0 then
		exit;
	if C < 32 then
	begin
		D := 32 - C;
		E := (A.LongWords[1] shr C) or (A.LongWords[0] shl D);
		F := (A.LongWords[0] shr C) or (A.LongWords[1] shl D);
	end
	else
	begin
		Dec(C, 32);
		D := 32 - C;
		E := (A.LongWords[0] shr C) or (A.LongWords[1] shl D);
		F := (A.LongWords[1] shr C) or (A.LongWords[0] shl D);
	end;
	A.LongWords[1] := E;
	A.LongWords[0] := F;
end;

procedure Word64SwapEndian(var A: Word64);
var
	B: Word64;
	I: Integer;
begin
	B              := A;
	for I          := 0 to 7 do
		A.Bytes[I] := B.Bytes[7 - I];
end;

procedure SwapEndianBuf64(var Buf; const Count: Integer);
var
	P: PWord64;
	I: Integer;
begin
	P     := @Buf;
	for I := 1 to Count do
	begin
		Word64SwapEndian(P^);
		Inc(P);
	end;
end;

{                                                                              }
{ MD5 hashing                                                                  }
{                                                                              }
const
	MD5Table_1: array [0 .. 15] of LongWord = (
	  $D76AA478, $E8C7B756, $242070DB, $C1BDCEEE,
	  $F57C0FAF, $4787C62A, $A8304613, $FD469501,
	  $698098D8, $8B44F7AF, $FFFF5BB1, $895CD7BE,
	  $6B901122, $FD987193, $A679438E, $49B40821);
	MD5Table_2: array [0 .. 15] of LongWord = (
	  $F61E2562, $C040B340, $265E5A51, $E9B6C7AA,
	  $D62F105D, $02441453, $D8A1E681, $E7D3FBC8,
	  $21E1CDE6, $C33707D6, $F4D50D87, $455A14ED,
	  $A9E3E905, $FCEFA3F8, $676F02D9, $8D2A4C8A);
	MD5Table_3: array [0 .. 15] of LongWord = (
	  $FFFA3942, $8771F681, $6D9D6122, $FDE5380C,
	  $A4BEEA44, $4BDECFA9, $F6BB4B60, $BEBFBC70,
	  $289B7EC6, $EAA127FA, $D4EF3085, $04881D05,
	  $D9D4D039, $E6DB99E5, $1FA27CF8, $C4AC5665);
	MD5Table_4: array [0 .. 15] of LongWord = (
	  $F4292244, $432AFF97, $AB9423A7, $FC93A039,
	  $655B59C3, $8F0CCC92, $FFEFF47D, $85845DD1,
	  $6FA87E4F, $FE2CE6E0, $A3014314, $4E0811A1,
	  $F7537E82, $BD3AF235, $2AD7D2BB, $EB86D391);

{ Calculates a MD5 Digest (16 bytes) given a Buffer (64 bytes)                 }
{$IFOPT Q+}{$DEFINE QOn}{$Q-}{$ELSE}{$UNDEF QOn}{$ENDIF}


procedure TransformMD5Buffer(var Digest: T128BitDigest; const Buffer);
var
	A, B, C, D: LongWord;
	P         : PLongWord;
	I         : Integer;
	J         : Byte;
	Buf       : array [0 .. 15] of LongWord absolute Buffer;
begin
	A := Digest.Longs[0];
	B := Digest.Longs[1];
	C := Digest.Longs[2];
	D := Digest.Longs[3];

	P     := @MD5Table_1;
	for I := 0 to 3 do
	begin
		J := I * 4;
		Inc(A, Buf[J] + P^ + (D xor (B and (C xor D))));
		A := A shl 7 or A shr 25 + B;
		Inc(P);
		Inc(D, Buf[J + 1] + P^ + (C xor (A and (B xor C))));
		D := D shl 12 or D shr 20 + A;
		Inc(P);
		Inc(C, Buf[J + 2] + P^ + (B xor (D and (A xor B))));
		C := C shl 17 or C shr 15 + D;
		Inc(P);
		Inc(B, Buf[J + 3] + P^ + (A xor (C and (D xor A))));
		B := B shl 22 or B shr 10 + C;
		Inc(P);
	end;

	P     := @MD5Table_2;
	for I := 0 to 3 do
	begin
		J := I * 4;
		Inc(A, Buf[J + 1] + P^ + (C xor (D and (B xor C))));
		A := A shl 5 or A shr 27 + B;
		Inc(P);
		Inc(D, Buf[(J + 6) mod 16] + P^ + (B xor (C and (A xor B))));
		D := D shl 9 or D shr 23 + A;
		Inc(P);
		Inc(C, Buf[(J + 11) mod 16] + P^ + (A xor (B and (D xor A))));
		C := C shl 14 or C shr 18 + D;
		Inc(P);
		Inc(B, Buf[J] + P^ + (D xor (A and (C xor D))));
		B := B shl 20 or B shr 12 + C;
		Inc(P);
	end;

	P     := @MD5Table_3;
	for I := 0 to 3 do
	begin
		J := 16 - (I * 4);
		Inc(A, Buf[(J + 5) mod 16] + P^ + (B xor C xor D));
		A := A shl 4 or A shr 28 + B;
		Inc(P);
		Inc(D, Buf[(J + 8) mod 16] + P^ + (A xor B xor C));
		D := D shl 11 or D shr 21 + A;
		Inc(P);
		Inc(C, Buf[(J + 11) mod 16] + P^ + (D xor A xor B));
		C := C shl 16 or C shr 16 + D;
		Inc(P);
		Inc(B, Buf[(J + 14) mod 16] + P^ + (C xor D xor A));
		B := B shl 23 or B shr 9 + C;
		Inc(P);
	end;

	P     := @MD5Table_4;
	for I := 0 to 3 do
	begin
		J := 16 - (I * 4);
		Inc(A, Buf[J mod 16] + P^ + (C xor (B or not D)));
		A := A shl 6 or A shr 26 + B;
		Inc(P);
		Inc(D, Buf[(J + 7) mod 16] + P^ + (B xor (A or not C)));
		D := D shl 10 or D shr 22 + A;
		Inc(P);
		Inc(C, Buf[(J + 14) mod 16] + P^ + (A xor (D or not B)));
		C := C shl 15 or C shr 17 + D;
		Inc(P);
		Inc(B, Buf[(J + 5) mod 16] + P^ + (D xor (C or not A)));
		B := B shl 21 or B shr 11 + C;
		Inc(P);
	end;

	Inc(Digest.Longs[0], A);
	Inc(Digest.Longs[1], B);
	Inc(Digest.Longs[2], C);
	Inc(Digest.Longs[3], D);
end;
{$IFDEF QOn}{$Q+}{$ENDIF}


procedure MD5InitDigest(var Digest: T128BitDigest);
begin
	Digest.Longs[0] := $67452301;
	Digest.Longs[1] := $EFCDAB89;
	Digest.Longs[2] := $98BADCFE;
	Digest.Longs[3] := $10325476;
end;

procedure MD5Buf(var Digest: T128BitDigest; const Buf; const BufSize: Integer);
var
	P   : PByte;
	I, J: Integer;
begin
	I := BufSize;
	if I <= 0 then
		exit;
	Assert(I mod 64 = 0, 'BufSize must be multiple of 64 bytes');
	P     := @Buf;
	for J := 0 to I div 64 - 1 do
	begin
		TransformMD5Buffer(Digest, P^);
		Inc(P, 64);
	end;
end;

procedure MD5FinalBuf(var Digest: T128BitDigest; const Buf; const BufSize: Integer; const TotalSize: Int64);
var
	B1, B2: T512BitBuf;
	C     : Integer;
begin
	StdFinalBuf512(Buf, BufSize, TotalSize, B1, B2, C, False);
	TransformMD5Buffer(Digest, B1);
	if C > 1 then
		TransformMD5Buffer(Digest, B2);
	SecureClear512(B1);
	if C > 1 then
		SecureClear512(B2);
end;

function CalcMD5(const Buf; const BufSize: Integer): T128BitDigest;
var
	I, J: Integer;
	P   : PByte;
begin
	MD5InitDigest(Result);
	P := @Buf;
	if BufSize <= 0 then
		I := 0
	else
		I := BufSize;
	J     := (I div 64) * 64;
	if J > 0 then
	begin
		MD5Buf(Result, P^, J);
		Inc(P, J);
		Dec(I, J);
	end;
	MD5FinalBuf(Result, P^, I, BufSize);
end;

function CalcMD5(const Buf: AnsiString): T128BitDigest;
begin
	Result := CalcMD5(Pointer(Buf)^, Length(Buf));
end;

function MD5DigestToStrA(const Digest: T128BitDigest): AnsiString;
begin
	SetLength(Result, Sizeof(Digest));
	Move(Digest, Pointer(Result)^, Sizeof(Digest));
end;

function MD5DigestToHexA(const Digest: T128BitDigest): AnsiString;
begin
	Result := DigestToHexA(Digest, Sizeof(Digest));
end;

function MD5DigestToHexW(const Digest: T128BitDigest): WideString;
begin
	Result := DigestToHexW(Digest, Sizeof(Digest));
end;

{                                                                              }
{ SHA hashing                                                                  }
{                                                                              }
procedure SHA1InitDigest(var Digest: T160BitDigest);
begin
	Digest.Longs[0] := $67452301;
	Digest.Longs[1] := $EFCDAB89;
	Digest.Longs[2] := $98BADCFE;
	Digest.Longs[3] := $10325476;
	Digest.Longs[4] := $C3D2E1F0;
end;

{ Calculates a SHA Digest (20 bytes) given a Buffer (64 bytes)                 }
{$IFOPT Q+}{$DEFINE QOn}{$Q-}{$ELSE}{$UNDEF QOn}{$ENDIF}


procedure TransformSHABuffer(var Digest: T160BitDigest; const Buffer; const SHA1: Boolean);
var
	A, B, C, D, E: LongWord;
	W            : array [0 .. 79] of LongWord;
	P, Q         : PLongWord;
	I            : Integer;
	J            : LongWord;
begin
	P     := @Buffer;
	Q     := @W;
	for I := 0 to 15 do
	begin
		Q^ := SwapEndian(P^);
		Inc(P);
		Inc(Q);
	end;
	for I := 0 to 63 do
	begin
		P := Q;
		Dec(P, 16);
		J := P^;
		Inc(P, 2);
		J := J xor P^;
		Inc(P, 6);
		J := J xor P^;
		Inc(P, 5);
		J := J xor P^;
		if SHA1 then
			J := RotateLeftBits(J, 1);
		Q^    := J;
		Inc(Q);
	end;

	A := Digest.Longs[0];
	B := Digest.Longs[1];
	C := Digest.Longs[2];
	D := Digest.Longs[3];
	E := Digest.Longs[4];

	P     := @W;
	for I := 0 to 3 do
	begin
		Inc(E, (A shl 5 or A shr 27) + (D xor (B and (C xor D))) + P^ + $5A827999);
		B := B shr 2 or B shl 30;
		Inc(P);
		Inc(D, (E shl 5 or E shr 27) + (C xor (A and (B xor C))) + P^ + $5A827999);
		A := A shr 2 or A shl 30;
		Inc(P);
		Inc(C, (D shl 5 or D shr 27) + (B xor (E and (A xor B))) + P^ + $5A827999);
		E := E shr 2 or E shl 30;
		Inc(P);
		Inc(B, (C shl 5 or C shr 27) + (A xor (D and (E xor A))) + P^ + $5A827999);
		D := D shr 2 or D shl 30;
		Inc(P);
		Inc(A, (B shl 5 or B shr 27) + (E xor (C and (D xor E))) + P^ + $5A827999);
		C := C shr 2 or C shl 30;
		Inc(P);
	end;

	for I := 0 to 3 do
	begin
		Inc(E, (A shl 5 or A shr 27) + (D xor B xor C) + P^ + $6ED9EBA1);
		B := B shr 2 or B shl 30;
		Inc(P);
		Inc(D, (E shl 5 or E shr 27) + (C xor A xor B) + P^ + $6ED9EBA1);
		A := A shr 2 or A shl 30;
		Inc(P);
		Inc(C, (D shl 5 or D shr 27) + (B xor E xor A) + P^ + $6ED9EBA1);
		E := E shr 2 or E shl 30;
		Inc(P);
		Inc(B, (C shl 5 or C shr 27) + (A xor D xor E) + P^ + $6ED9EBA1);
		D := D shr 2 or D shl 30;
		Inc(P);
		Inc(A, (B shl 5 or B shr 27) + (E xor C xor D) + P^ + $6ED9EBA1);
		C := C shr 2 or C shl 30;
		Inc(P);
	end;

	for I := 0 to 3 do
	begin
		Inc(E, (A shl 5 or A shr 27) + ((B and C) or (D and (B or C))) + P^ + $8F1BBCDC);
		B := B shr 2 or B shl 30;
		Inc(P);
		Inc(D, (E shl 5 or E shr 27) + ((A and B) or (C and (A or B))) + P^ + $8F1BBCDC);
		A := A shr 2 or A shl 30;
		Inc(P);
		Inc(C, (D shl 5 or D shr 27) + ((E and A) or (B and (E or A))) + P^ + $8F1BBCDC);
		E := E shr 2 or E shl 30;
		Inc(P);
		Inc(B, (C shl 5 or C shr 27) + ((D and E) or (A and (D or E))) + P^ + $8F1BBCDC);
		D := D shr 2 or D shl 30;
		Inc(P);
		Inc(A, (B shl 5 or B shr 27) + ((C and D) or (E and (C or D))) + P^ + $8F1BBCDC);
		C := C shr 2 or C shl 30;
		Inc(P);
	end;

	for I := 0 to 3 do
	begin
		Inc(E, (A shl 5 or A shr 27) + (D xor B xor C) + P^ + $CA62C1D6);
		B := B shr 2 or B shl 30;
		Inc(P);
		Inc(D, (E shl 5 or E shr 27) + (C xor A xor B) + P^ + $CA62C1D6);
		A := A shr 2 or A shl 30;
		Inc(P);
		Inc(C, (D shl 5 or D shr 27) + (B xor E xor A) + P^ + $CA62C1D6);
		E := E shr 2 or E shl 30;
		Inc(P);
		Inc(B, (C shl 5 or C shr 27) + (A xor D xor E) + P^ + $CA62C1D6);
		D := D shr 2 or D shl 30;
		Inc(P);
		Inc(A, (B shl 5 or B shr 27) + (E xor C xor D) + P^ + $CA62C1D6);
		C := C shr 2 or C shl 30;
		Inc(P);
	end;

	Inc(Digest.Longs[0], A);
	Inc(Digest.Longs[1], B);
	Inc(Digest.Longs[2], C);
	Inc(Digest.Longs[3], D);
	Inc(Digest.Longs[4], E);
end;
{$IFDEF QOn}{$Q+}{$ENDIF}


procedure SHA1Buf(var Digest: T160BitDigest; const Buf; const BufSize: Integer);
var
	P   : PByte;
	I, J: Integer;
begin
	I := BufSize;
	if I <= 0 then
		exit;
	Assert(I mod 64 = 0, 'BufSize must be multiple of 64 bytes');
	P     := @Buf;
	for J := 0 to I div 64 - 1 do
	begin
		TransformSHABuffer(Digest, P^, True);
		Inc(P, 64);
	end;
end;

procedure SHA1FinalBuf(var Digest: T160BitDigest; const Buf; const BufSize: Integer; const TotalSize: Int64);
var
	B1, B2: T512BitBuf;
	C     : Integer;
begin
	StdFinalBuf512(Buf, BufSize, TotalSize, B1, B2, C, True);
	TransformSHABuffer(Digest, B1, True);
	if C > 1 then
		TransformSHABuffer(Digest, B2, True);
	SwapEndianBuf(Digest, Sizeof(Digest) div Sizeof(LongWord));
	SecureClear512(B1);
	if C > 1 then
		SecureClear512(B2);
end;

function CalcSHA1(const Buf; const BufSize: Integer): T160BitDigest;
var
	I, J: Integer;
	P   : PByte;
begin
	SHA1InitDigest(Result);
	P := @Buf;
	if BufSize <= 0 then
		I := 0
	else
		I := BufSize;
	J     := (I div 64) * 64;
	if J > 0 then
	begin
		SHA1Buf(Result, P^, J);
		Inc(P, J);
		Dec(I, J);
	end;
	SHA1FinalBuf(Result, P^, I, BufSize);
end;

function CalcSHA1(const Buf: AnsiString): T160BitDigest;
begin
	Result := CalcSHA1(Pointer(Buf)^, Length(Buf));
end;

function SHA1DigestToStrA(const Digest: T160BitDigest): AnsiString;
begin
	SetLength(Result, Sizeof(Digest));
	Move(Digest, Pointer(Result)^, Sizeof(Digest));
end;

function SHA1DigestToHexA(const Digest: T160BitDigest): AnsiString;
begin
	Result := DigestToHexA(Digest, Sizeof(Digest));
end;

function SHA1DigestToHexW(const Digest: T160BitDigest): WideString;
begin
	Result := DigestToHexW(Digest, Sizeof(Digest));
end;

{                                                                              }
{ SHA224 Hashing                                                               }
{                                                                              }
{ SHA-224 is identical to SHA-256, except that:                                }
{ - the initial variable values h0 through h7 are different, and               }
{ - the output is constructed by omitting h7                                   }
{                                                                              }
procedure SHA224InitDigest(var Digest: T256BitDigest);
begin
  // The second 32 bits of the fractional parts of the square roots of the 9th through 16th primes 23..53
	Digest.Longs[0] := $C1059ED8;
	Digest.Longs[1] := $367CD507;
	Digest.Longs[2] := $3070DD17;
	Digest.Longs[3] := $F70E5939;
	Digest.Longs[4] := $FFC00B31;
	Digest.Longs[5] := $68581511;
	Digest.Longs[6] := $64F98FA7;
	Digest.Longs[7] := $BEFA4FA4;
end;

procedure SHA224Buf(var Digest: T256BitDigest; const Buf; const BufSize: Integer);
begin
	SHA256Buf(Digest, Buf, BufSize);
end;

procedure SHA224FinalBuf(var Digest: T256BitDigest; const Buf; const BufSize: Integer; const TotalSize: Int64;
  var OutDigest: T224BitDigest);
begin
	SHA256FinalBuf(Digest, Buf, BufSize, TotalSize);
	Move(Digest.Longs[0], OutDigest.Longs[0], SizeOf(T224BitDigest));
end;

function CalcSHA224(const Buf; const BufSize: Integer): T224BitDigest;
var
	D   : T256BitDigest;
	I, J: Integer;
	P   : PByte;
begin
	SHA224InitDigest(D);
	P := @Buf;
	if BufSize <= 0 then
		I := 0
	else
		I := BufSize;
	J     := (I div 64) * 64;
	if J > 0 then
	begin
		SHA224Buf(D, P^, J);
		Inc(P, J);
		Dec(I, J);
	end;
	SHA224FinalBuf(D, P^, I, BufSize, Result);
end;

function CalcSHA224(const Buf: AnsiString): T224BitDigest;
begin
	Result := CalcSHA224(Pointer(Buf)^, Length(Buf));
end;

function SHA224DigestToStrA(const Digest: T224BitDigest): AnsiString;
begin
	SetLength(Result, Sizeof(Digest));
	Move(Digest, Pointer(Result)^, Sizeof(Digest));
end;

function SHA224DigestToHexA(const Digest: T224BitDigest): AnsiString;
begin
	Result := DigestToHexA(Digest, Sizeof(Digest));
end;

function SHA224DigestToHexW(const Digest: T224BitDigest): WideString;
begin
	Result := DigestToHexW(Digest, Sizeof(Digest));
end;

{                                                                              }
{ SHA256 hashing                                                               }
{                                                                              }
procedure SHA256InitDigest(var Digest: T256BitDigest);
begin
	Digest.Longs[0] := $6A09E667;
	Digest.Longs[1] := $BB67AE85;
	Digest.Longs[2] := $3C6EF372;
	Digest.Longs[3] := $A54FF53A;
	Digest.Longs[4] := $510E527F;
	Digest.Longs[5] := $9B05688C;
	Digest.Longs[6] := $1F83D9AB;
	Digest.Longs[7] := $5BE0CD19;
end;

function SHA256Transform1(const A: LongWord): LongWord;
begin
	Result := RotateRightBits(A, 7) xor RotateRightBits(A, 18) xor (A shr 3);
end;

function SHA256Transform2(const A: LongWord): LongWord;
begin
	Result := RotateRightBits(A, 17) xor RotateRightBits(A, 19) xor (A shr 10);
end;

function SHA256Transform3(const A: LongWord): LongWord;
begin
	Result := RotateRightBits(A, 2) xor RotateRightBits(A, 13) xor RotateRightBits(A, 22);
end;

function SHA256Transform4(const A: LongWord): LongWord;
begin
	Result := RotateRightBits(A, 6) xor RotateRightBits(A, 11) xor RotateRightBits(A, 25);
end;

const
  // first 32 bits of the fractional parts of the cube roots of the first 64 primes 2..311
	SHA256K: array [0 .. 63] of LongWord = (
	  $428A2F98, $71374491, $B5C0FBCF, $E9B5DBA5, $3956C25B, $59F111F1, $923F82A4, $AB1C5ED5,
	  $D807AA98, $12835B01, $243185BE, $550C7DC3, $72BE5D74, $80DEB1FE, $9BDC06A7, $C19BF174,
	  $E49B69C1, $EFBE4786, $0FC19DC6, $240CA1CC, $2DE92C6F, $4A7484AA, $5CB0A9DC, $76F988DA,
	  $983E5152, $A831C66D, $B00327C8, $BF597FC7, $C6E00BF3, $D5A79147, $06CA6351, $14292967,
	  $27B70A85, $2E1B2138, $4D2C6DFC, $53380D13, $650A7354, $766A0ABB, $81C2C92E, $92722C85,
	  $A2BFE8A1, $A81A664B, $C24B8B70, $C76C51A3, $D192E819, $D6990624, $F40E3585, $106AA070,
	  $19A4C116, $1E376C08, $2748774C, $34B0BCB5, $391C0CB3, $4ED8AA4A, $5B9CCA4F, $682E6FF3,
	  $748F82EE, $78A5636F, $84C87814, $8CC70208, $90BEFFFA, $A4506CEB, $BEF9A3F7, $C67178F2
	  );

{$IFOPT Q+}{$DEFINE QOn}{$Q-}{$ELSE}{$UNDEF QOn}{$ENDIF}


procedure TransformSHA256Buffer(var Digest: T256BitDigest; const Buf);
var
	I                      : Integer;
	W                      : array [0 .. 63] of LongWord;
	P                      : PLongWord;
	S0, S1, Maj, T1, T2, Ch: LongWord;
	H                      : array [0 .. 7] of LongWord;
begin
	P     := @Buf;
	for I := 0 to 15 do
	begin
		W[I] := SwapEndian(P^);
		Inc(P);
	end;
	for I := 16 to 63 do
	begin
		S0   := SHA256Transform1(W[I - 15]);
		S1   := SHA256Transform2(W[I - 2]);
		W[I] := W[I - 16] + S0 + W[I - 7] + S1;
	end;
	for I    := 0 to 7 do
		H[I] := Digest.Longs[I];
	for I    := 0 to 63 do
	begin
		S0   := SHA256Transform3(H[0]);
		Maj  := (H[0] and H[1]) xor (H[0] and H[2]) xor (H[1] and H[2]);
		T2   := S0 + Maj;
		S1   := SHA256Transform4(H[4]);
		Ch   := (H[4] and H[5]) xor ((not H[4]) and H[6]);
		T1   := H[7] + S1 + Ch + SHA256K[I] + W[I];
		H[7] := H[6];
		H[6] := H[5];
		H[5] := H[4];
		H[4] := H[3] + T1;
		H[3] := H[2];
		H[2] := H[1];
		H[1] := H[0];
		H[0] := T1 + T2;
	end;
	for I := 0 to 7 do
		Inc(Digest.Longs[I], H[I]);
end;
{$IFDEF QOn}{$Q+}{$ENDIF}


procedure SHA256Buf(var Digest: T256BitDigest; const Buf; const BufSize: Integer);
var
	P   : PByte;
	I, J: Integer;
begin
	I := BufSize;
	if I <= 0 then
		exit;
	Assert(I mod 64 = 0, 'BufSize must be multiple of 64 bytes');
	P     := @Buf;
	for J := 0 to I div 64 - 1 do
	begin
		TransformSHA256Buffer(Digest, P^);
		Inc(P, 64);
	end;
end;

procedure SHA256FinalBuf(var Digest: T256BitDigest; const Buf; const BufSize: Integer; const TotalSize: Int64);
var
	B1, B2: T512BitBuf;
	C     : Integer;
begin
	StdFinalBuf512(Buf, BufSize, TotalSize, B1, B2, C, True);
	TransformSHA256Buffer(Digest, B1);
	if C > 1 then
		TransformSHA256Buffer(Digest, B2);
	SwapEndianBuf(Digest, Sizeof(Digest) div Sizeof(LongWord));
	SecureClear512(B1);
	if C > 1 then
		SecureClear512(B2);
end;

function CalcSHA256(const Buf; const BufSize: Integer): T256BitDigest;
var
	I, J: Integer;
	P   : PByte;
begin
	SHA256InitDigest(Result);
	P := @Buf;
	if BufSize <= 0 then
		I := 0
	else
		I := BufSize;
	J     := (I div 64) * 64;
	if J > 0 then
	begin
		SHA256Buf(Result, P^, J);
		Inc(P, J);
		Dec(I, J);
	end;
	SHA256FinalBuf(Result, P^, I, BufSize);
end;

function CalcSHA256(const Buf: AnsiString): T256BitDigest;
begin
	Result := CalcSHA256(Pointer(Buf)^, Length(Buf));
end;

function SHA256DigestToStrA(const Digest: T256BitDigest): AnsiString;
begin
	SetLength(Result, Sizeof(Digest));
	Move(Digest, Pointer(Result)^, Sizeof(Digest));
end;

function SHA256DigestToHexA(const Digest: T256BitDigest): AnsiString;
begin
	Result := DigestToHexA(Digest, Sizeof(Digest));
end;

function SHA256DigestToHexW(const Digest: T256BitDigest): WideString;
begin
	Result := DigestToHexW(Digest, Sizeof(Digest));
end;

{                                                                              }
{ SHA384 Hashing                                                               }
{                                                                              }
procedure SHA384InitDigest(var Digest: T512BitDigest);
begin
	Digest.Word64s[0].LongWords[0] := $C1059ED8;
	Digest.Word64s[0].LongWords[1] := $CBBB9D5D;
	Digest.Word64s[1].LongWords[0] := $367CD507;
	Digest.Word64s[1].LongWords[1] := $629A292A;
	Digest.Word64s[2].LongWords[0] := $3070DD17;
	Digest.Word64s[2].LongWords[1] := $9159015A;
	Digest.Word64s[3].LongWords[0] := $F70E5939;
	Digest.Word64s[3].LongWords[1] := $152FECD8;
	Digest.Word64s[4].LongWords[0] := $FFC00B31;
	Digest.Word64s[4].LongWords[1] := $67332667;
	Digest.Word64s[5].LongWords[0] := $68581511;
	Digest.Word64s[5].LongWords[1] := $8EB44A87;
	Digest.Word64s[6].LongWords[0] := $64F98FA7;
	Digest.Word64s[6].LongWords[1] := $DB0C2E0D;
	Digest.Word64s[7].LongWords[0] := $BEFA4FA4;
	Digest.Word64s[7].LongWords[1] := $47B5481D;
end;

procedure SHA384Buf(var Digest: T512BitDigest; const Buf; const BufSize: Integer);
begin
	SHA512Buf(Digest, Buf, BufSize);
end;

procedure SHA384FinalBuf(var Digest: T512BitDigest; const Buf; const BufSize: Integer; const TotalSize: Int64; var OutDigest: T384BitDigest);
begin
	SHA512FinalBuf(Digest, Buf, BufSize, TotalSize);
	Move(Digest, OutDigest, SizeOf(OutDigest));
end;

function CalcSHA384(const Buf; const BufSize: Integer): T384BitDigest;
var
	I, J: Integer;
	P   : PByte;
	D   : T512BitDigest;
begin
	SHA384InitDigest(D);
	P := @Buf;
	if BufSize <= 0 then
		I := 0
	else
		I := BufSize;
	J     := (I div 128) * 128;
	if J > 0 then
	begin
		SHA384Buf(D, P^, J);
		Inc(P, J);
		Dec(I, J);
	end;
	SHA384FinalBuf(D, P^, I, BufSize, Result);
end;

function CalcSHA384(const Buf: AnsiString): T384BitDigest;
begin
	Result := CalcSHA384(Pointer(Buf)^, Length(Buf));
end;

function SHA384DigestToStrA(const Digest: T384BitDigest): AnsiString;
begin
	SetLength(Result, Sizeof(Digest));
	Move(Digest, Pointer(Result)^, Sizeof(Digest));
end;

function SHA384DigestToHexA(const Digest: T384BitDigest): AnsiString;
begin
	Result := DigestToHexA(Digest, Sizeof(Digest));
end;

function SHA384DigestToHexW(const Digest: T384BitDigest): WideString;
begin
	Result := DigestToHexW(Digest, Sizeof(Digest));
end;

{                                                                              }
{ SHA512 Hashing                                                               }
{                                                                              }
procedure SHA512InitDigest(var Digest: T512BitDigest);
begin
	Digest.Word64s[0].LongWords[0] := $F3BCC908;
	Digest.Word64s[0].LongWords[1] := $6A09E667;
	Digest.Word64s[1].LongWords[0] := $84CAA73B;
	Digest.Word64s[1].LongWords[1] := $BB67AE85;
	Digest.Word64s[2].LongWords[0] := $FE94F82B;
	Digest.Word64s[2].LongWords[1] := $3C6EF372;
	Digest.Word64s[3].LongWords[0] := $5F1D36F1;
	Digest.Word64s[3].LongWords[1] := $A54FF53A;
	Digest.Word64s[4].LongWords[0] := $ADE682D1;
	Digest.Word64s[4].LongWords[1] := $510E527F;
	Digest.Word64s[5].LongWords[0] := $2B3E6C1F;
	Digest.Word64s[5].LongWords[1] := $9B05688C;
	Digest.Word64s[6].LongWords[0] := $FB41BD6B;
	Digest.Word64s[6].LongWords[1] := $1F83D9AB;
	Digest.Word64s[7].LongWords[0] := $137E2179;
	Digest.Word64s[7].LongWords[1] := $5BE0CD19;
end;

// BSIG0(x) = ROTR^28(x) XOR ROTR^34(x) XOR ROTR^39(x)
function SHA512Transform1(const A: Word64): Word64;
var
	T1, T2, T3: Word64;
begin
	T1 := A;
	T2 := A;
	T3 := A;
	Word64Ror(T1, 28);
	Word64Ror(T2, 34);
	Word64Ror(T3, 39);
	Word64XorWord64(T1, T2);
	Word64XorWord64(T1, T3);
	Result := T1;
end;

// BSIG1(x) = ROTR^14(x) XOR ROTR^18(x) XOR ROTR^41(x)
function SHA512Transform2(const A: Word64): Word64;
var
	T1, T2, T3: Word64;
begin
	T1 := A;
	T2 := A;
	T3 := A;
	Word64Ror(T1, 14);
	Word64Ror(T2, 18);
	Word64Ror(T3, 41);
	Word64XorWord64(T1, T2);
	Word64XorWord64(T1, T3);
	Result := T1;
end;

// SSIG0(x) = ROTR^1(x) XOR ROTR^8(x) XOR SHR^7(x)
function SHA512Transform3(const A: Word64): Word64;
var
	T1, T2, T3: Word64;
begin
	T1 := A;
	T2 := A;
	T3 := A;
	Word64Ror(T1, 1);
	Word64Ror(T2, 8);
	Word64Shr(T3, 7);
	Word64XorWord64(T1, T2);
	Word64XorWord64(T1, T3);
	Result := T1;
end;

// SSIG1(x) = ROTR^19(x) XOR ROTR^61(x) XOR SHR^6(x)
function SHA512Transform4(const A: Word64): Word64;
var
	T1, T2, T3: Word64;
begin
	T1 := A;
	T2 := A;
	T3 := A;
	Word64Ror(T1, 19);
	Word64Ror(T2, 61);
	Word64Shr(T3, 6);
	Word64XorWord64(T1, T2);
	Word64XorWord64(T1, T3);
	Result := T1;
end;

// CH( x, y, z) = (x AND y) XOR ( (NOT x) AND z)
function SHA512Transform5(const X, Y, Z: Word64): Word64;
var
	T1, T2: Word64;
begin
	T1 := X;
	Word64AndWord64(T1, Y);
	T2 := X;
	Word64Not(T2);
	Word64AndWord64(T2, Z);
	Word64XorWord64(T1, T2);
	Result := T1;
end;

// MAJ( x, y, z) = (x AND y) XOR (x AND z) XOR (y AND z)
function SHA512Transform6(const X, Y, Z: Word64): Word64;
var
	T1, T2, T3: Word64;
begin
	T1 := X;
	Word64AndWord64(T1, Y);
	T2 := X;
	Word64AndWord64(T2, Z);
	T3 := Y;
	Word64AndWord64(T3, Z);
	Word64XorWord64(T1, T2);
	Word64XorWord64(T1, T3);
	Result := T1;
end;

const
  // first 64 bits of the fractional parts of the cube roots of the first eighty prime numbers
  // (stored High LongWord first then Low LongWord)
	SHA512K: array [0 .. 159] of LongWord = (
	  $428A2F98, $D728AE22, $71374491, $23EF65CD, $B5C0FBCF, $EC4D3B2F, $E9B5DBA5, $8189DBBC,
	  $3956C25B, $F348B538, $59F111F1, $B605D019, $923F82A4, $AF194F9B, $AB1C5ED5, $DA6D8118,
	  $D807AA98, $A3030242, $12835B01, $45706FBE, $243185BE, $4EE4B28C, $550C7DC3, $D5FFB4E2,
	  $72BE5D74, $F27B896F, $80DEB1FE, $3B1696B1, $9BDC06A7, $25C71235, $C19BF174, $CF692694,
	  $E49B69C1, $9EF14AD2, $EFBE4786, $384F25E3, $0FC19DC6, $8B8CD5B5, $240CA1CC, $77AC9C65,
	  $2DE92C6F, $592B0275, $4A7484AA, $6EA6E483, $5CB0A9DC, $BD41FBD4, $76F988DA, $831153B5,
	  $983E5152, $EE66DFAB, $A831C66D, $2DB43210, $B00327C8, $98FB213F, $BF597FC7, $BEEF0EE4,
	  $C6E00BF3, $3DA88FC2, $D5A79147, $930AA725, $06CA6351, $E003826F, $14292967, $0A0E6E70,
	  $27B70A85, $46D22FFC, $2E1B2138, $5C26C926, $4D2C6DFC, $5AC42AED, $53380D13, $9D95B3DF,
	  $650A7354, $8BAF63DE, $766A0ABB, $3C77B2A8, $81C2C92E, $47EDAEE6, $92722C85, $1482353B,
	  $A2BFE8A1, $4CF10364, $A81A664B, $BC423001, $C24B8B70, $D0F89791, $C76C51A3, $0654BE30,
	  $D192E819, $D6EF5218, $D6990624, $5565A910, $F40E3585, $5771202A, $106AA070, $32BBD1B8,
	  $19A4C116, $B8D2D0C8, $1E376C08, $5141AB53, $2748774C, $DF8EEB99, $34B0BCB5, $E19B48A8,
	  $391C0CB3, $C5C95A63, $4ED8AA4A, $E3418ACB, $5B9CCA4F, $7763E373, $682E6FF3, $D6B2B8A3,
	  $748F82EE, $5DEFB2FC, $78A5636F, $43172F60, $84C87814, $A1F0AB72, $8CC70208, $1A6439EC,
	  $90BEFFFA, $23631E28, $A4506CEB, $DE82BDE9, $BEF9A3F7, $B2C67915, $C67178F2, $E372532B,
	  $CA273ECE, $EA26619C, $D186B8C7, $21C0C207, $EADA7DD6, $CDE0EB1E, $F57D4F7F, $EE6ED178,
	  $06F067AA, $72176FBA, $0A637DC5, $A2C898A6, $113F9804, $BEF90DAE, $1B710B35, $131C471B,
	  $28DB77F5, $23047D84, $32CAAB7B, $40C72493, $3C9EBE0A, $15C9BEBC, $431D67C4, $9C100D4C,
	  $4CC5D4BE, $CB3E42B6, $597F299C, $FC657E2A, $5FCB6FAB, $3AD6FAEC, $6C44198C, $4A475817
	  );

{$IFOPT Q+}{$DEFINE QOn}{$Q-}{$ELSE}{$UNDEF QOn}{$ENDIF}


procedure TransformSHA512Buffer(var Digest: T512BitDigest; const Buf);
var
	I                : Integer;
	P                : PWord64;
	W                : array [0 .. 79] of Word64;
	T1, T2, T3, T4, K: Word64;
	H                : array [0 .. 7] of Word64;
begin
	P     := @Buf;
	for I := 0 to 15 do
	begin
		W[I] := P^;
		Word64SwapEndian(W[I]);
		Inc(P);
	end;
	for I := 16 to 79 do
	begin
		T1 := SHA512Transform4(W[I - 2]);
		T2 := W[I - 7];
		T3 := SHA512Transform3(W[I - 15]); // bug in RFC (specifies I-5 instead of W[I-5])
		T4 := W[I - 16];
		Word64AddWord64(T1, T2);
		Word64AddWord64(T1, T3);
		Word64AddWord64(T1, T4);
		W[I] := T1;
	end;
	for I    := 0 to 7 do
		H[I] := Digest.Word64s[I];
	for I    := 0 to 79 do
	begin
      // T1 = h + BSIG1(e) + CH(e,f,g) + Kt + Wt
		T1 := H[7];
		Word64AddWord64(T1, SHA512Transform2(H[4]));
		Word64AddWord64(T1, SHA512Transform5(H[4], H[5], H[6]));
		K.LongWords[0] := SHA512K[I * 2 + 1];
		K.LongWords[1] := SHA512K[I * 2];
		Word64AddWord64(T1, K);
		Word64AddWord64(T1, W[I]);
      // T2 = BSIG0(a) + MAJ(a,b,c)
		T2 := SHA512Transform1(H[0]);
		Word64AddWord64(T2, SHA512Transform6(H[0], H[1], H[2]));
      // h = g    g = f
      // f = e    e = d + T1
      // d = c    c = b
      // b = a    a = T1 + T2
		H[7] := H[6];
		H[6] := H[5];
		H[5] := H[4];
		H[4] := H[3];
		Word64AddWord64(H[4], T1);
		H[3] := H[2];
		H[2] := H[1];
		H[1] := H[0];
		H[0] := T1;
		Word64AddWord64(H[0], T2);
	end;
	for I := 0 to 7 do
		Word64AddWord64(Digest.Word64s[I], H[I]);
end;
{$IFDEF QOn}{$Q+}{$ENDIF}


procedure SHA512Buf(var Digest: T512BitDigest; const Buf; const BufSize: Integer);
var
	P   : PByte;
	I, J: Integer;
begin
	I := BufSize;
	if I <= 0 then
		exit;
	Assert(I mod 128 = 0, 'BufSize must be multiple of 128 bytes');
	P     := @Buf;
	for J := 0 to I div 128 - 1 do
	begin
		TransformSHA512Buffer(Digest, P^);
		Inc(P, 128);
	end;
end;

procedure SHA512FinalBuf(var Digest: T512BitDigest; const Buf; const BufSize: Integer; const TotalSize: Int64);
var
	B1, B2: T1024BitBuf;
	C     : Integer;
begin
	StdFinalBuf1024(Buf, BufSize, TotalSize, B1, B2, C, True);
	TransformSHA512Buffer(Digest, B1);
	if C > 1 then
		TransformSHA512Buffer(Digest, B2);
	SwapEndianBuf64(Digest, Sizeof(Digest) div Sizeof(Word64));
	SecureClear1024(B1);
	if C > 1 then
		SecureClear1024(B2);
end;

function CalcSHA512(const Buf; const BufSize: Integer): T512BitDigest;
var
	I, J: Integer;
	P   : PByte;
begin
	SHA512InitDigest(Result);
	P := @Buf;
	if BufSize <= 0 then
		I := 0
	else
		I := BufSize;
	J     := (I div 128) * 128;
	if J > 0 then
	begin
		SHA512Buf(Result, P^, J);
		Inc(P, J);
		Dec(I, J);
	end;
	SHA512FinalBuf(Result, P^, I, BufSize);
end;

function CalcSHA512(const Buf: AnsiString): T512BitDigest;
begin
	Result := CalcSHA512(Pointer(Buf)^, Length(Buf));
end;

function SHA512DigestToStrA(const Digest: T512BitDigest): AnsiString;
begin
	SetLength(Result, Sizeof(Digest));
	Move(Digest, Pointer(Result)^, Sizeof(Digest));
end;

function SHA512DigestToHexA(const Digest: T512BitDigest): AnsiString;
begin
	Result := DigestToHexA(Digest, Sizeof(Digest));
end;

function SHA512DigestToHexW(const Digest: T512BitDigest): WideString;
begin
	Result := DigestToHexW(Digest, Sizeof(Digest));
end;

{                                                                              }
{ HMAC utility functions                                                       }
{                                                                              }
procedure HMAC_KeyBlock512(const Key; const KeySize: Integer; var Buf: T512BitBuf);
var
	P: PAnsiChar;
begin
	Assert(KeySize <= 64);
	P := @Buf;
	if KeySize > 0 then
	begin
		Move(Key, P^, KeySize);
		Inc(P, KeySize);
	end;
	FillChar(P^, 64 - KeySize, #0);
end;

procedure HMAC_KeyBlock1024(const Key; const KeySize: Integer; var Buf: T1024BitBuf);
var
	P: PAnsiChar;
begin
	Assert(KeySize <= 128);
	P := @Buf;
	if KeySize > 0 then
	begin
		Move(Key, P^, KeySize);
		Inc(P, KeySize);
	end;
	FillChar(P^, 128 - KeySize, #0);
end;

procedure XORBlock512(var Buf: T512BitBuf; const XOR8: Byte);
var
	I: Integer;
begin
	for I      := 0 to SizeOf(Buf) - 1 do
		Buf[I] := Buf[I] xor XOR8;
end;

procedure XORBlock1024(var Buf: T1024BitBuf; const XOR8: Byte);
var
	I: Integer;
begin
	for I      := 0 to SizeOf(Buf) - 1 do
		Buf[I] := Buf[I] xor XOR8;
end;

{                                                                              }
{ HMAC-MD5 keyed hashing                                                       }
{                                                                              }
procedure HMAC_MD5Init(const Key: Pointer; const KeySize: Integer; var Digest: T128BitDigest; var K: T512BitBuf);
var
	S: T512BitBuf;
	D: T128BitDigest;
begin
	MD5InitDigest(Digest);

	if KeySize > 64 then
	begin
		D := CalcMD5(Key^, KeySize);
		HMAC_KeyBlock512(D, Sizeof(D), K);
	end
	else
		HMAC_KeyBlock512(Key^, KeySize, K);

	Move(K, S, SizeOf(K));
	XORBlock512(S, $36);
	TransformMD5Buffer(Digest, S);
	SecureClear512(S);
end;

procedure HMAC_MD5Buf(var Digest: T128BitDigest; const Buf; const BufSize: Integer);
begin
	MD5Buf(Digest, Buf, BufSize);
end;

procedure HMAC_MD5FinalBuf(const K: T512BitBuf; var Digest: T128BitDigest; const Buf; const BufSize: Integer; const TotalSize: Int64);
var
	FinBuf: packed record
	  K   : T512BitBuf;
	D     : T128BitDigest;
end;
begin
	MD5FinalBuf(Digest, Buf, BufSize, TotalSize + 64);
	Move(K, FinBuf.K, SizeOf(K));
	XORBlock512(FinBuf.K, $5C);
	Move(Digest, FinBuf.D, SizeOf(Digest));
	Digest := CalcMD5(FinBuf, SizeOf(FinBuf));
	SecureClear(FinBuf, SizeOf(FinBuf));
end;

function CalcHMAC_MD5(const Key: Pointer; const KeySize: Integer; const Buf; const BufSize: Integer): T128BitDigest;
var
	I, J: Integer;
	P   : PByte;
	K   : T512BitBuf;
begin
	HMAC_MD5Init(Key, KeySize, Result, K);
	P := @Buf;
	if BufSize <= 0 then
		I := 0
	else
		I := BufSize;
	J     := (I div 64) * 64;
	if J > 0 then
	begin
		HMAC_MD5Buf(Result, P^, J);
		Inc(P, J);
		Dec(I, J);
	end;
	HMAC_MD5FinalBuf(K, Result, P^, I, BufSize);
	SecureClear512(K);
end;

function CalcHMAC_MD5(const Key: AnsiString; const Buf; const BufSize: Integer): T128BitDigest;
begin
	Result := CalcHMAC_MD5(Pointer(Key), Length(Key), Buf, BufSize);
end;

function CalcHMAC_MD5(const Key, Buf: AnsiString): T128BitDigest;
begin
	Result := CalcHMAC_MD5(Key, Pointer(Buf)^, Length(Buf));
end;

{                                                                              }
{ HMAC-SHA1 keyed hashing                                                      }
{                                                                              }
procedure HMAC_SHA1Init(const Key: Pointer; const KeySize: Integer; var Digest: T160BitDigest; var K: T512BitBuf);
var
	D: T160BitDigest;
	S: T512BitBuf;
begin
	SHA1InitDigest(Digest);

	if KeySize > 64 then
	begin
		D := CalcSHA1(Key^, KeySize);
		HMAC_KeyBlock512(D, Sizeof(D), K);
	end
	else
		HMAC_KeyBlock512(Key^, KeySize, K);

	Move(K, S, SizeOf(K));
	XORBlock512(S, $36);
	TransformSHABuffer(Digest, S, True);
	SecureClear512(S);
end;

procedure HMAC_SHA1Buf(var Digest: T160BitDigest; const Buf; const BufSize: Integer);
begin
	SHA1Buf(Digest, Buf, BufSize);
end;

procedure HMAC_SHA1FinalBuf(const K: T512BitBuf; var Digest: T160BitDigest; const Buf; const BufSize: Integer; const TotalSize: Int64);
var
	FinBuf: packed record
	  K   : T512BitBuf;
	D     : T160BitDigest;
end;
begin
	SHA1FinalBuf(Digest, Buf, BufSize, TotalSize + 64);
	Move(K, FinBuf.K, SizeOf(K));
	XORBlock512(FinBuf.K, $5C);
	Move(Digest, FinBuf.D, SizeOf(Digest));
	Digest := CalcSHA1(FinBuf, SizeOf(FinBuf));
	SecureClear(FinBuf, SizeOf(FinBuf));
end;

function CalcHMAC_SHA1(const Key: Pointer; const KeySize: Integer; const Buf; const BufSize: Integer): T160BitDigest;
var
	I, J: Integer;
	P   : PByte;
	K   : T512BitBuf;
begin
	HMAC_SHA1Init(Key, KeySize, Result, K);
	P := @Buf;
	if BufSize <= 0 then
		I := 0
	else
		I := BufSize;
	J     := (I div 64) * 64;
	if J > 0 then
	begin
		HMAC_SHA1Buf(Result, P^, J);
		Inc(P, J);
		Dec(I, J);
	end;
	HMAC_SHA1FinalBuf(K, Result, P^, I, BufSize);
	SecureClear512(K);
end;

function CalcHMAC_SHA1(const Key: AnsiString; const Buf; const BufSize: Integer): T160BitDigest;
begin
	Result := CalcHMAC_SHA1(Pointer(Key), Length(Key), Buf, BufSize);
end;

function CalcHMAC_SHA1(const Key, Buf: AnsiString): T160BitDigest;
begin
	Result := CalcHMAC_SHA1(Key, Pointer(Buf)^, Length(Buf));
end;

{                                                                              }
{ HMAC-SHA256 keyed hashing                                                    }
{                                                                              }
procedure HMAC_SHA256Init(const Key: Pointer; const KeySize: Integer; var Digest: T256BitDigest; var K: T512BitBuf);
var
	D: T256BitDigest;
	S: T512BitBuf;
begin
	SHA256InitDigest(Digest);

	if KeySize > 64 then
	begin
		D := CalcSHA256(Key^, KeySize);
		HMAC_KeyBlock512(D, Sizeof(D), K);
	end
	else
		HMAC_KeyBlock512(Key^, KeySize, K);

	Move(K, S, SizeOf(K));
	XORBlock512(S, $36);
	TransformSHA256Buffer(Digest, S);
	SecureClear512(S);
end;

procedure HMAC_SHA256Buf(var Digest: T256BitDigest; const Buf; const BufSize: Integer);
begin
	SHA256Buf(Digest, Buf, BufSize);
end;

procedure HMAC_SHA256FinalBuf(const K: T512BitBuf; var Digest: T256BitDigest; const Buf; const BufSize: Integer; const TotalSize: Int64);
var
	FinBuf: packed record
	  K   : T512BitBuf;
	D     : T256BitDigest;
end;
begin
	SHA256FinalBuf(Digest, Buf, BufSize, TotalSize + 64);
	Move(K, FinBuf.K, SizeOf(K));
	XORBlock512(FinBuf.K, $5C);
	Move(Digest, FinBuf.D, SizeOf(Digest));
	Digest := CalcSHA256(FinBuf, SizeOf(FinBuf));
	SecureClear(FinBuf, SizeOf(FinBuf));
end;

function CalcHMAC_SHA256(const Key: Pointer; const KeySize: Integer; const Buf; const BufSize: Integer): T256BitDigest;
var
	I, J: Integer;
	P   : PByte;
	K   : T512BitBuf;
begin
	HMAC_SHA256Init(Key, KeySize, Result, K);
	P := @Buf;
	if BufSize <= 0 then
		I := 0
	else
		I := BufSize;
	J     := (I div 64) * 64;
	if J > 0 then
	begin
		HMAC_SHA256Buf(Result, P^, J);
		Inc(P, J);
		Dec(I, J);
	end;
	HMAC_SHA256FinalBuf(K, Result, P^, I, BufSize);
	SecureClear512(K);
end;

function CalcHMAC_SHA256(const Key: AnsiString; const Buf; const BufSize: Integer): T256BitDigest;
begin
	Result := CalcHMAC_SHA256(Pointer(Key), Length(Key), Buf, BufSize);
end;

function CalcHMAC_SHA256(const Key, Buf: AnsiString): T256BitDigest;
begin
	Result := CalcHMAC_SHA256(Key, Pointer(Buf)^, Length(Buf));
end;

{                                                                              }
{ HMAC-SHA512 keyed hashing                                                    }
{                                                                              }
procedure HMAC_SHA512Init(const Key: Pointer; const KeySize: Integer; var Digest: T512BitDigest; var K: T1024BitBuf);
var
	D: T512BitDigest;
	S: T1024BitBuf;
begin
	SHA512InitDigest(Digest);

	if KeySize > 128 then
	begin
		D := CalcSHA512(Key^, KeySize);
		HMAC_KeyBlock1024(D, Sizeof(D), K);
	end
	else
		HMAC_KeyBlock1024(Key^, KeySize, K);

	Move(K, S, SizeOf(K));
	XORBlock1024(S, $36);
	TransformSHA512Buffer(Digest, S);
	SecureClear1024(S);
end;

procedure HMAC_SHA512Buf(var Digest: T512BitDigest; const Buf; const BufSize: Integer);
begin
	SHA512Buf(Digest, Buf, BufSize);
end;

procedure HMAC_SHA512FinalBuf(const K: T1024BitBuf; var Digest: T512BitDigest; const Buf; const BufSize: Integer; const TotalSize: Int64);
var
	FinBuf: packed record
	  K   : T1024BitBuf;
	D     : T512BitDigest;
end;
begin
	SHA512FinalBuf(Digest, Buf, BufSize, TotalSize + 128);
	Move(K, FinBuf.K, SizeOf(K));
	XORBlock1024(FinBuf.K, $5C);
	Move(Digest, FinBuf.D, SizeOf(Digest));
	Digest := CalcSHA512(FinBuf, SizeOf(FinBuf));
	SecureClear(FinBuf, SizeOf(FinBuf));
end;

function CalcHMAC_SHA512(const Key: Pointer; const KeySize: Integer; const Buf; const BufSize: Integer): T512BitDigest;
var
	I, J: Integer;
	P   : PByte;
	K   : T1024BitBuf;
begin
	HMAC_SHA512Init(Key, KeySize, Result, K);
	P := @Buf;
	if BufSize <= 0 then
		I := 0
	else
		I := BufSize;
	J     := (I div 128) * 128;
	if J > 0 then
	begin
		HMAC_SHA512Buf(Result, P^, J);
		Inc(P, J);
		Dec(I, J);
	end;
	HMAC_SHA512FinalBuf(K, Result, P^, I, BufSize);
	SecureClear1024(K);
end;

function CalcHMAC_SHA512(const Key: AnsiString; const Buf; const BufSize: Integer): T512BitDigest;
begin
	Result := CalcHMAC_SHA512(Pointer(Key), Length(Key), Buf, BufSize);
end;

function CalcHMAC_SHA512(const Key, Buf: AnsiString): T512BitDigest;
begin
	Result := CalcHMAC_SHA512(Key, Pointer(Buf)^, Length(Buf));
end;

{                                                                              }
{ CalculateHash                                                                }
{                                                                              }
procedure CalculateHash(const HashType: THashType;
  const Buf; const BufSize: Integer;
  const Digest: Pointer;
  const Key: Pointer; const KeySize: Integer);
begin
	if KeySize > 0 then
		case HashType of
			hashHMAC_MD5:
			P128BitDigest(Digest)^ := CalcHMAC_MD5(Key, KeySize, Buf, BufSize);
			hashHMAC_SHA1:
			P160BitDigest(Digest)^ := CalcHMAC_SHA1(Key, KeySize, Buf, BufSize);
			hashHMAC_SHA256:
			P256BitDigest(Digest)^ := CalcHMAC_SHA256(Key, KeySize, Buf, BufSize);
			hashHMAC_SHA512:
			P512BitDigest(Digest)^ := CalcHMAC_SHA512(Key, KeySize, Buf, BufSize);
			else
			raise EHashError.Create(hashNotKeyedHashType);
		end
	else
		case HashType of
			hashChecksum32:
			PLongWord(Digest)^ := CalcChecksum32(Buf, BufSize);
			hashXOR8:
			PByte(Digest)^ := CalcXOR8(Buf, BufSize);
			hashXOR16:
			PWord(Digest)^ := CalcXOR16(Buf, BufSize);
			hashXOR32:
			PLongWord(Digest)^ := CalcXOR32(Buf, BufSize);
			hashCRC16:
			PWord(Digest)^ := CalcCRC16(Buf, BufSize);
			hashCRC32:
			PLongWord(Digest)^ := CalcCRC32(Buf, BufSize);
			hashMD5:
			P128BitDigest(Digest)^ := CalcMD5(Buf, BufSize);
			hashSHA1:
			P160BitDigest(Digest)^ := CalcSHA1(Buf, BufSize);
			hashSHA256:
			P256BitDigest(Digest)^ := CalcSHA256(Buf, BufSize);
			hashSHA512:
			P512BitDigest(Digest)^ := CalcSHA512(Buf, BufSize);
			hashHMAC_MD5:
			P128BitDigest(Digest)^ := CalcHMAC_MD5(nil, 0, Buf, BufSize);
			hashHMAC_SHA1:
			P160BitDigest(Digest)^ := CalcHMAC_SHA1(nil, 0, Buf, BufSize);
			hashHMAC_SHA256:
			P256BitDigest(Digest)^ := CalcHMAC_SHA256(nil, 0, Buf, BufSize);
			hashHMAC_SHA512:
			P512BitDigest(Digest)^ := CalcHMAC_SHA512(nil, 0, Buf, BufSize);
			else
			raise EHashError.Create(hashInvalidHashType);
		end;
end;

procedure CalculateHash(const HashType: THashType; const Buf; const BufSize: Integer; const Digest: Pointer; const Key: AnsiString);
begin
	CalculateHash(HashType, Buf, BufSize, Digest, Pointer(Key), Length(Key));
end;

procedure CalculateHash(const HashType: THashType; const Buf: AnsiString; const Digest: Pointer; const Key: AnsiString);
begin
	CalculateHash(HashType, Pointer(Buf)^, Length(Buf), Digest, Key);
end;

{                                                                              }
{ System helper functions                                                      }
{                                                                              }
resourcestring
	SSystemError = 'System error #%s';

{                                                                              }
{ AHash                                                                        }
{                                                                              }
class function AHash.BlockSize: Integer;
begin
	Result := - 1;
end;

procedure AHash.ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64);
begin
	ProcessBuf(Buf, BufSize);
end;

procedure AHash.Init(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	Assert(Assigned(Digest));
	FDigest    := Digest;
	FTotalSize := 0;
	InitHash(Digest, Key, KeySize);
end;

procedure AHash.Init(const Digest: Pointer; const Key: AnsiString);
begin
	Init(Digest, Pointer(Key), Length(Key));
end;

procedure AHash.HashBuf(const Buf; const BufSize: Integer; const FinalBuf: Boolean);
var
	I, D: Integer;
	P   : PAnsiChar;
begin
	Inc(FTotalSize, BufSize);

	D := BlockSize;
	if D < 0 then
		D := 64;
	P     := @Buf;
	I     := (BufSize div D) * D;
	if I > 0 then
	begin
		ProcessBuf(P^, I);
		Inc(P, I);
	end;

	I := BufSize mod D;
	if FinalBuf then
		ProcessFinalBuf(P^, I, FTotalSize)
	else
	  if I > 0 then
		raise EHashError.Create(hashInvalidBufferSize, 'Non final buffer must be multiple of block size');
end;

procedure AHash.HashFile(const FileName: string; const Offset: Int64; const MaxCount: Int64);
const
	ChunkSize = 8192;
var
	Handle: Integer;
	Buf   : Pointer;
	I, C  : Integer;
	Left  : Int64;
	Fin   : Boolean;
begin
	if FileName = '' then
		raise EHashError.Create(hashInvalidFileName);
	Handle := FileOpen(FileName, fmOpenReadWrite or fmShareDenyNone);
	if Handle = - 1 then
		raise EHashError.Create(hashFileOpenError, GetLastErrorText);
	if Offset > 0 then
		I := FileSeek(Handle, Offset, 0)
	else
	  if Offset < 0 then
		I := FileSeek(Handle, Offset, 2)
	else
		I := 0;
	if I = - 1 then
		raise EHashError.Create(hashFileSeekError, GetLastErrorText);
	try
		GetMem(Buf, ChunkSize);
		try
			if MaxCount < 0 then
				Left := high(Int64)
			else
				Left := MaxCount;
			repeat
				if Left > ChunkSize then
					C := ChunkSize
				else
					C := Left;
				if C = 0 then
				begin
					I   := 0;
					Fin := True;
				end else
				begin
					I := FileRead(Handle, Buf^, C);
					if I = - 1 then
						raise EHashError.Create(hashFileReadError, GetLastErrorText);
					Dec(Left, I);
					Fin := (I < C) or (Left <= 0);
				end;
				HashBuf(Buf^, I, Fin);
			until Fin;
		finally
			FreeMem(Buf, ChunkSize);
		end;
	finally
		FileClose(Handle);
	end;
end;

{                                                                              }
{ TChecksum32Hash                                                              }
{                                                                              }
procedure TChecksum32Hash.InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	PLongWord(Digest)^ := 0;
end;

procedure TChecksum32Hash.ProcessBuf(const Buf; const BufSize: Integer);
begin
	PLongWord(FDigest)^ := PLongWord(FDigest)^ + CalcChecksum32(Buf, BufSize);
end;

class function TChecksum32Hash.DigestSize: Integer;
begin
	Result := 4;
end;

{                                                                              }
{ TXOR8Hash                                                                    }
{                                                                              }
procedure TXOR8Hash.InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	PByte(Digest)^ := 0;
end;

procedure TXOR8Hash.ProcessBuf(const Buf; const BufSize: Integer);
begin
	PByte(FDigest)^ := PByte(FDigest)^ xor CalcXOR8(Buf, BufSize);
end;

class function TXOR8Hash.DigestSize: Integer;
begin
	Result := 1;
end;

{                                                                              }
{ TXOR16Hash                                                                   }
{                                                                              }
procedure TXOR16Hash.InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	PWord(Digest)^ := 0;
end;

procedure TXOR16Hash.ProcessBuf(const Buf; const BufSize: Integer);
begin
	PWord(FDigest)^ := PWord(FDigest)^ xor CalcXOR16(Buf, BufSize);
end;

class function TXOR16Hash.DigestSize: Integer;
begin
	Result := 2;
end;

{                                                                              }
{ TXOR32Hash                                                                   }
{                                                                              }
procedure TXOR32Hash.InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	PLongWord(Digest)^ := 0;
end;

procedure TXOR32Hash.ProcessBuf(const Buf; const BufSize: Integer);
begin
	PLongWord(FDigest)^ := PLongWord(FDigest)^ xor CalcXOR32(Buf, BufSize);
end;

class function TXOR32Hash.DigestSize: Integer;
begin
	Result := 4;
end;

{                                                                              }
{ TCRC16Hash                                                                   }
{                                                                              }
procedure TCRC16Hash.InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	CRC16Init(PWord(Digest)^);
end;

procedure TCRC16Hash.ProcessBuf(const Buf; const BufSize: Integer);
begin
	PWord(FDigest)^ := CRC16Buf(PWord(FDigest)^, Buf, BufSize);
end;

class function TCRC16Hash.DigestSize: Integer;
begin
	Result := 2;
end;

{                                                                              }
{ TCRC32Hash                                                                   }
{                                                                              }
procedure TCRC32Hash.InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	CRC32Init(PLongWord(Digest)^);
end;

procedure TCRC32Hash.ProcessBuf(const Buf; const BufSize: Integer);
begin
	PLongWord(FDigest)^ := CRC32Buf(PLongWord(FDigest)^, Buf, BufSize);
end;

class function TCRC32Hash.DigestSize: Integer;
begin
	Result := 4;
end;

{                                                                              }
{ TAdler32Hash                                                                 }
{                                                                              }
procedure TAdler32Hash.InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	Adler32Init(PLongWord(Digest)^);
end;

procedure TAdler32Hash.ProcessBuf(const Buf; const BufSize: Integer);
begin
	PLongWord(FDigest)^ := Adler32Buf(PLongWord(FDigest)^, Buf, BufSize);
end;

class function TAdler32Hash.DigestSize: Integer;
begin
	Result := 4;
end;

{                                                                              }
{ TELFHash                                                                     }
{                                                                              }
procedure TELFHash.InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	ELFInit(PLongWord(Digest)^);
end;

procedure TELFHash.ProcessBuf(const Buf; const BufSize: Integer);
begin
	PLongWord(FDigest)^ := ELFBuf(PLongWord(FDigest)^, Buf, BufSize);
end;

class function TELFHash.DigestSize: Integer;
begin
	Result := 4;
end;

{                                                                              }
{ TMD5Hash                                                                     }
{                                                                              }
procedure TMD5Hash.InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	MD5InitDigest(P128BitDigest(FDigest)^);
end;

procedure TMD5Hash.ProcessBuf(const Buf; const BufSize: Integer);
begin
	MD5Buf(P128BitDigest(FDigest)^, Buf, BufSize);
end;

procedure TMD5Hash.ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64);
begin
	MD5FinalBuf(P128BitDigest(FDigest)^, Buf, BufSize, TotalSize);
end;

class function TMD5Hash.DigestSize: Integer;
begin
	Result := 16;
end;

class function TMD5Hash.BlockSize: Integer;
begin
	Result := 64;
end;

{                                                                              }
{ TSHA1Hash                                                                    }
{                                                                              }
procedure TSHA1Hash.InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	SHA1InitDigest(P160BitDigest(FDigest)^);
end;

procedure TSHA1Hash.ProcessBuf(const Buf; const BufSize: Integer);
begin
	SHA1Buf(P160BitDigest(FDigest)^, Buf, BufSize);
end;

procedure TSHA1Hash.ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64);
begin
	SHA1FinalBuf(P160BitDigest(FDigest)^, Buf, BufSize, TotalSize);
end;

class function TSHA1Hash.DigestSize: Integer;
begin
	Result := 20;
end;

class function TSHA1Hash.BlockSize: Integer;
begin
	Result := 64;
end;

{                                                                              }
{ TSHA256Hash                                                                  }
{                                                                              }
procedure TSHA256Hash.InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	SHA256InitDigest(P256BitDigest(FDigest)^);
end;

procedure TSHA256Hash.ProcessBuf(const Buf; const BufSize: Integer);
begin
	SHA256Buf(P256BitDigest(FDigest)^, Buf, BufSize);
end;

procedure TSHA256Hash.ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64);
begin
	SHA256FinalBuf(P256BitDigest(FDigest)^, Buf, BufSize, TotalSize);
end;

class function TSHA256Hash.DigestSize: Integer;
begin
	Result := 32;
end;

class function TSHA256Hash.BlockSize: Integer;
begin
	Result := 64;
end;

{                                                                              }
{ TSHA512Hash                                                                  }
{                                                                              }
procedure TSHA512Hash.InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	SHA512InitDigest(P512BitDigest(FDigest)^);
end;

procedure TSHA512Hash.ProcessBuf(const Buf; const BufSize: Integer);
begin
	SHA512Buf(P512BitDigest(FDigest)^, Buf, BufSize);
end;

procedure TSHA512Hash.ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64);
begin
	SHA512FinalBuf(P512BitDigest(FDigest)^, Buf, BufSize, TotalSize);
end;

class function TSHA512Hash.DigestSize: Integer;
begin
	Result := 64;
end;

class function TSHA512Hash.BlockSize: Integer;
begin
	Result := 128;
end;

{                                                                              }
{ THMAC_MD5Hash                                                                }
{                                                                              }
destructor THMAC_MD5Hash.Destroy;
begin
	SecureClear512(FKey);
	inherited Destroy;
end;

procedure THMAC_MD5Hash.InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	HMAC_MD5Init(Key, KeySize, P128BitDigest(FDigest)^, FKey);
end;

procedure THMAC_MD5Hash.ProcessBuf(const Buf; const BufSize: Integer);
begin
	HMAC_MD5Buf(P128BitDigest(FDigest)^, Buf, BufSize);
end;

procedure THMAC_MD5Hash.ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64);
begin
	HMAC_MD5FinalBuf(FKey, P128BitDigest(FDigest)^, Buf, BufSize, TotalSize);
end;

class function THMAC_MD5Hash.DigestSize: Integer;
begin
	Result := 16;
end;

class function THMAC_MD5Hash.BlockSize: Integer;
begin
	Result := 64;
end;

{                                                                              }
{ THMAC_SHA1Hash                                                               }
{                                                                              }
destructor THMAC_SHA1Hash.Destroy;
begin
	SecureClear512(FKey);
	inherited Destroy;
end;

procedure THMAC_SHA1Hash.InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	HMAC_SHA1Init(Key, KeySize, P160BitDigest(FDigest)^, FKey);
end;

procedure THMAC_SHA1Hash.ProcessBuf(const Buf; const BufSize: Integer);
begin
	HMAC_SHA1Buf(P160BitDigest(FDigest)^, Buf, BufSize);
end;

procedure THMAC_SHA1Hash.ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64);
begin
	HMAC_SHA1FinalBuf(FKey, P160BitDigest(FDigest)^, Buf, BufSize, TotalSize);
end;

class function THMAC_SHA1Hash.DigestSize: Integer;
begin
	Result := 20;
end;

class function THMAC_SHA1Hash.BlockSize: Integer;
begin
	Result := 64;
end;

{                                                                              }
{ THMAC_SHA256Hash                                                             }
{                                                                              }
destructor THMAC_SHA256Hash.Destroy;
begin
	SecureClear512(FKey);
	inherited Destroy;
end;

procedure THMAC_SHA256Hash.InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	HMAC_SHA256Init(Key, KeySize, P256BitDigest(FDigest)^, FKey);
end;

procedure THMAC_SHA256Hash.ProcessBuf(const Buf; const BufSize: Integer);
begin
	HMAC_SHA256Buf(P256BitDigest(FDigest)^, Buf, BufSize);
end;

procedure THMAC_SHA256Hash.ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64);
begin
	HMAC_SHA256FinalBuf(FKey, P256BitDigest(FDigest)^, Buf, BufSize, TotalSize);
end;

class function THMAC_SHA256Hash.DigestSize: Integer;
begin
	Result := 32;
end;

class function THMAC_SHA256Hash.BlockSize: Integer;
begin
	Result := 64;
end;

{                                                                              }
{ THMAC_SHA512Hash                                                             }
{                                                                              }
destructor THMAC_SHA512Hash.Destroy;
begin
	SecureClear1024(FKey);
	inherited Destroy;
end;

procedure THMAC_SHA512Hash.InitHash(const Digest: Pointer; const Key: Pointer; const KeySize: Integer);
begin
	HMAC_SHA512Init(Key, KeySize, P512BitDigest(FDigest)^, FKey);
end;

procedure THMAC_SHA512Hash.ProcessBuf(const Buf; const BufSize: Integer);
begin
	HMAC_SHA512Buf(P512BitDigest(FDigest)^, Buf, BufSize);
end;

procedure THMAC_SHA512Hash.ProcessFinalBuf(const Buf; const BufSize: Integer; const TotalSize: Int64);
begin
	HMAC_SHA512FinalBuf(FKey, P512BitDigest(FDigest)^, Buf, BufSize, TotalSize);
end;

class function THMAC_SHA512Hash.DigestSize: Integer;
begin
	Result := 64;
end;

class function THMAC_SHA512Hash.BlockSize: Integer;
begin
	Result := 128;
end;

{                                                                              }
{ HashString                                                                   }
{                                                                              }
function HashString(const StrBuf: Pointer; const StrLength: Integer; const Slots: LongWord; const CaseSensitive: Boolean): LongWord;
var
	P   : PAnsiChar;
	I, J: Integer;

	procedure CRC32StrBuf(const Size: Integer);
	begin
		if CaseSensitive then
			Result := CRC32Buf(Result, P^, Size)
		else
			Result := CRC32BufNoCase(Result, P^, Size);
	end;

begin
  // Return 0 for an empty string
	Result := 0;
	if (StrLength <= 0) or not Assigned(StrBuf) then
		exit;

	if not CRC32TableInit then
		InitCRC32Table;
	Result := $FFFFFFFF;
	P      := StrBuf;

	if StrLength <= 48 then // Hash everything for short strings
		CRC32StrBuf(StrLength)
	else
	begin
      // Hash first 16 bytes
		CRC32StrBuf(16);

      // Hash last 16 bytes
		Inc(P, StrLength - 16);
		CRC32StrBuf(16);

      // Hash 16 bytes sampled from rest of string
		I := (StrLength - 48) div 16;
		P := StrBuf;
		Inc(P, 16);
		for J := 1 to 16 do
		begin
			CRC32StrBuf(1);
			Inc(P, I + 1);
		end;
	end;

  // Mod into slots
	if (Slots <> 0) and (Slots <> high(LongWord)) then
		Result := Result mod Slots;
end;

function HashString(const S: AnsiString; const Slots: LongWord; const CaseSensitive: Boolean): LongWord;
begin
	Result := HashString(Pointer(S), Length(S), Slots, CaseSensitive);
end;

{                                                                              }
{ Hash by THashType                                                            }
{                                                                              }
const
	HashTypeClasses: array [THashType] of THashClass = (
	  TChecksum32Hash, TXOR8Hash, TXOR16Hash, TXOR32Hash,
	  TCRC16Hash, TCRC32Hash,
	  TAdler32Hash,
	  TELFHash,
	  TMD5Hash, TSHA1Hash, TSHA256Hash, TSHA512Hash,
	  THMAC_MD5Hash, THMAC_SHA1Hash, THMAC_SHA256Hash, THMAC_SHA512Hash);

function GetHashClassByType(const HashType: THashType): THashClass;
begin
	Result := HashTypeClasses[HashType];
end;

function GetDigestSize(const HashType: THashType): Integer;
begin
	Result := GetHashClassByType(HashType).DigestSize;
end;

end.
